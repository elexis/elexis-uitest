--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: ecl_helpers
Element-Type: context
Element-Version: 2.0
Id: _Cr4zgP0jEeiNqfuAT0PA2A
Runtime-Version: 2.4.0.201812130013
Save-Time: 1/9/19 10:41 AM

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Here you find some ECL helper functions to solve often used problems.
------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc "get-env" [val name] {
		invoke-static "org.eclipse.rcptt.util" "java.lang.System" getenv $name
}
proc "pwd" {
  // invoke-static "org.eclipse.rcptt.util" -className java.nio.file.Paths -methodName get -args "."
  get-java-property "user.dir"
}

proc "get-default-locale" {
	invoke-static "org.eclipse.rcptt.util" "java.util.Locale" getDefault
}

proc "get-user-dir" {
	if [ get-by-os  -win true -macosx false -linux false ] {
		global [val myUserDir [ concat  [get-java-property user.home] "/elexis" | str ] ] -override
		// concat "global1 myUserDir: " $myUserDir |log
		global [val myUserDir [ replaceAll  -replaceString  "/" -searchPattern  "C:" -inString $myUserDir] ] -override
		global [val myUserDir [ replaceAll  -replaceString  "/" -searchPattern  "\\\\" -inString $myUserDir] ] -override
		// concat "global3 myUserDir: " $myUserDir |log
		global [val myUserDir [ replaceAll  -replaceString  "\\\\ " -searchPattern  " " -inString $myUserDir] ] -override
		concat "get-user-dir return: " $myUserDir |log
		$myUserDir | str
	} -else {
		invoke-static -pluginId ch.elexis.core.data -className ch.elexis.core.data.activator.CoreHub -methodName getWritableUserDir
    }
}
proc "safe-get-java-property" [val name] {
	try {
		log -message [ concat "Value of Java property " $name " is: " [ get-java-property $name ] ]
		get-java-property $name
	} -catch {
	    log [ concat "No such java-property: " $name ]
	    ""
	}
}

proc "get-seconds" {
	 invoke-static "org.eclipse.rcptt.util" "java.lang.System" currentTimeMillis | div 1000
}

/**
 * @input: inString a string
*/
proc "replaceFirst" [ val inString ] [ val searchPattern ] [ val replaceString] {
	$inString | invoke replaceFirst $searchPattern $replaceString
}
// replaceFirst "Welcome to Tutorialspoint.com" "(.*)Tutorials(.*)" "AMROOD" | log

proc "replaceAll" [ val inString ] [ val searchPattern ] [ val replaceString] {
	$inString | invoke replaceAll $searchPattern $replaceString
}

global [ val resultsDir   [ concat  [ pwd ]  "/../results/"] ]
global [ val translations [ read-properties -uri [concat "file:" [get-user-dir] "/rcptt/translations.properties"]]]
global [ val imagesDir    [ concat $resultsDir "images/" ] ]
global [ val preferencesCsvFile [ concat  $resultsDir "preferences.csv" ] ]
global [ val viewsCsvFile       [ concat  $resultsDir "views.csv" ] ]
concat "get-default-locale returns: " [get-default-locale] |log
concat "get-user-dir returns: " [get-user-dir] |log
concat "user.language returns: " [ get-java-property "user.language" "dummy" ]  |log
global [ val defaultLanguage [ get-java-property "user.language" "de" ] ]
concat "defaultLanguage is now: " $defaultLanguage| log
$translations | get "de.open"| eq "Öffnen" | verify-true
proc "t" [val key] [val language $defaultLanguage] {
	try {
		// concat "Searching: " $language "." $key | log
		$translations | get [ concat $language "." $key] 
		} -catch {
		concat "Unable to fetch key " [ concat $language "." $key] " in translation.properties"  | log -severity Error
		bool false
	}
}
t "open" "de" | eq "Öffnen" | verify-true
t "open" "fr" | eq "Ouvrir" | verify-true

proc OpenAndResetStartPerspektive {
	get-button [ t btn_start_perspective ]  | click
	get-menu -path [ t menu_reset_perspective ] | click
	get-window [ t reset_perspective ] | get-button  [ t yes ]  | click
}

proc "DeleteAllArtikelstammItems" {
	str "delete from ARTIKELSTAMM_CH;" | write-lines $sqlCmdFile
	try {
		exec-process "java" "-classpath" $h2Jar "org.h2.tools.RunScript" "-url" $jdbcConnection "-user" "sa" "-script" $sqlCmdFile
	} -catch {
		log "Some error while deleting Aritiklestamm"
	}
	log "DeleteAllArtikelstammItems done"
}
proc "ReadArtikelstamm" [ val xmlFile "~/elexis/rcptt/Artikelstamm_Short_v5.xml" ] {
	concat "Restarting before Reading Artikelstamm file: " $xmlFile | log
	get-button "Artikel.*öffnen" | click
	with [get-view Artikel] {
	    get-tab-folder | get-tab-item Artikelstamm | click
	    get-editbox -after [get-label "Artikel oder Wirkstoff"] | set-focus
	}
	get-button "Menü anzeigen" | click
	get-view Artikel | get-menu -path "Import..." | click
	with [get-window Datenimport] {
	    get-button "Pharma-Artikel" | check
	    with [get-editbox -after [get-label "Bitte Datei angeben:"]] {
	        set-text $xmlFile
        }
        get-button OK | click
    }

	exec-with-options {
	    get-view Artikel | get-editbox -after [get-label "Artikel oder Wirkstoff"] | set-focus
	    try {
			get-window "Fehler aufgetreten" | verify-false
		} -catch {
			concat "No error while importing artikelstamm: " $xmlFile
		}
	} -allowStatusDialog
	// 19:25:06.595 [Worker-4] INFO  a.m.c.a.e.c.i.ArtikelstammImporter - ArtikelstammImporterPage.doImport /home/niklaus/elexis/rcptt/Artikelstamm_Short_v5.xml
	// 19:25:15.072 [Worker-4] INFO  a.m.c.a.e.c.i.ArtikelstammImporter - [PI] Leistungsblock validation finished
	get-view Artikel | get-button "Automatische Suche aktivieren" | click
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ArtikelstammImporter_1.png"]
	wait 1200 // 10 seconds are enough for the Artikelstamm_Short_v5
	get-view Artikel | get-button "Automatische Suche aktivieren" | click
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ArtikelstammImporter_2.png"]
	get-view Artikel | get-table | get-item -path "ASPIRIN S Tabl 500 mg 20 Stk <8.7> " | get-property caption
    | equals "ASPIRIN S Tabl 500 mg 20 Stk <8.7>" | verify-true
	concat "Read Artiklestamm done: " $imagesDir |log
}
//
// Ende

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

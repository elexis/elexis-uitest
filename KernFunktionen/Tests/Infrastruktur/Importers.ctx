--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Importers
Element-Type: context
Element-Version: 2.0
Id: _7pLWIBQVEemEb8FG0wciYA
Runtime-Version: 2.4.0.201901240010
Save-Time: 1/25/19 10:02 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

// We try to increase here some timeout to let artikelstamm and tarmed importer complete successfully
//	set-q7-option uijobHangTimeout       30000
//	set-q7-option jobSleepingStepTimeout 120000
// https://www.eclipse.org/forums/index.php/m/1734168/?srch=wait+progress+view#msg_1734168
// with 1000 and 2000 the OK danach arrived after 2 seconds
// 21:32:16.991 [Worker-28] INFO  PLATFORM - [org.eclipse.core.runtime] ReadArtikelstamm OK before q7
// 21:32:18.135 [Worker-22] INFO  PLATFORM - [org.eclipse.core.runtime] ReadArtikelstamm OK danach

// Achtung: Nur für demoDB gedacht und dort läuft es auch nicht wie geplant,
// da die Anpassungen beim Import immer noch auf dem alten Wert steht
proc "SetArtikelstammOrigin2oddb2ml" {
    if [ get-file $sqlCmdFile | get exists ] {
		delete-file $sqlCmdFile
		concat "deleted: " $sqlCmdFile | log
	}
	str "UPDATE ARTIKELSTAMM_CH set  ADDDSCR= 'oddb2ml' where ID like 'VERSION' and ADDDSCR = 'medindex'; ---" | write-lines $sqlCmdFile
	try {
		exec-process "java" "-classpath" $h2Jar "org.h2.tools.RunScript" "-url" $jdbcConnection "-user" "sa" "-script" $sqlCmdFile
	} -catch {
		log "Some error while deleting ARTIKELSTAMM_CH"
	}
	concat "DeleteAllArtikelstammItems done via: " $sqlCmdFile | log
}

proc "ExecuteDataImport" {
	with [get-window [ t view_data_import ]] {
	    get-button [ t btn_data_import_search ] | click
	    get-button OK | click
	}
}

proc "ReadArtikelstamm" [ val file [ concat  [get-user-dir] "/rcptt/Artikelstamm_Short_v5.xml" ] ] {
	open-window-by-name [ t daten_artikel ]
	concat "before ReadArtikelstamm file: " $file | log
	with [get-view [ t artikel ]] {
	    get-tab-folder | get-tab-item Artikelstamm | click
	    get-editbox -after [get-label [ t artikelstamm_label  ]] | set-focus
	}
	get-button [ t show_menu ] | click
	get-view [ t artikel ] | get-menu -path [ t btn_import ] | click
	set-dialog-result File $file
    concat "ReadArtikelstamm OK before " $file | log
	with [get-window [ t view_data_import ]] {
	    get-button [ t btn_data_import_search ] | click
	    // TODO: Wait that pull request 174 for Tickets 14745 gets merged
	    // get-button "Pharma-Artikel" | check
        get-button OK | click
    }

    concat "ReadArtikelstamm OK danach " $file | log
    
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ArtikelstammImporter_1.png"]
	// 19:25:06.595 [Worker-4] INFO  a.m.c.a.e.c.i.ArtikelstammImporter - ArtikelstammImporterPage.doImport /home/niklaus/elexis/rcptt/Artikelstamm_Short_v5.xml
	// 19:25:15.072 [Worker-4] INFO  a.m.c.a.e.c.i.ArtikelstammImporter - [PI] Leistungsblock validation finished
	get-view [ t artikel ] | get-button "Automatische Suche aktivieren" | click
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ArtikelstammImporter_2.png"]
	concat "ReadArtikelstamm done: " $file |log
}

proc "ReadBlocksExchange" [ val file  [ concat  [get-user-dir] "/rcptt/blocks_landarztpraxis_sevelen.xchange"] ] {
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ReadBlocksExchange_0.png"]
	get-button [ t btn_services ] | click
	concat "ReadBlocksExchange file is: " $file | log
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ReadBlocksExchange_1.png"]
	with [get-view Codes] {
	    get-tab-folder | get-tab-item [ t blocks ] | click
	    get-editbox -after [get-label Name] | set-focus
	}
	get-button [ t show_menu ] | click
	get-view Codes | get-menu -path [ t btn_import ] | click
	set-dialog-result File $file
	ExecuteDataImport
}
proc "ReadAnalysenListe" [ val file [ concat  [get-user-dir] "/rcptt/EAL_01012019.xlsx" ] ] {
	get-button [ t btn_services ] | click
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ReadEAL_1.png"]
	get-view Codes | get-tab-folder | get-tab-item "EAL 2009" | click
	concat "ReadAnalysenListe file is: " $file | log
	get-button [ t show_menu ] | click
	get-view Codes | get-menu -path [ t btn_import ] | click
	set-dialog-result File $file
	ExecuteDataImport
}

proc "ReadMigel" [ val file [ concat  [get-user-dir] "/rcptt/MiGeL-01012019.xlsx" ] ] {
	open-window-by-name [ t daten_artikel ]
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ReadMigel_1.png"]
	get-view [ t artikel ] | get-tab-folder | get-tab-item MiGeL | click
	get-view [ t artikel ] | get-menu -path [ t btn_import ] | click
	set-dialog-result File $file
	get-window [ t view_data_import ] | get-button [ t delete_all_data ] | check
	ExecuteDataImport
}

proc "ReadTarmed" [ val file  [ concat  [get-user-dir] "/rcptt/TARMED__Datenbank_01.09.00_BR_KVG-27.12.2017.mdb" ]  ] {
	get-button [ t btn_services ] | click
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ReadTarmed_1.png"]
	with [get-view Codes] {
	    get-tab-folder | get-tab-item Tarmed | click
	    get-editbox -after [get-label Ziffer] | set-focus
	}
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ReadTarmed_2.png"]
	get-button [ t show_menu ] | click
	get-view Codes | get-menu -path [ t btn_import ] | click
	set-dialog-result File $file
	with [get-window [ t view_data_import ]] {
	    get-button [ t btn_data_import_search ] | click
	    with [get-combo -after [get-label [ t relevant_tarmed_caselaw ] ] ] {
	        set-text KVG
	        select KVG
	    }
	    concat "XXXXStarting ReadTarmed with file: " $file | log
	    get-button OK | click
	    concat "XXXXDone ReadTarmed with file: " $file | log
	}
	try {
		get-window "Import erfolgreich" | get-button OK | click
		log "ReadTarmed: Closed dialog Import erfolgreich"
	} -catch {
		log "ReadTarmed:  dialog Import erfolgreich not found"
	}
}

proc "ReadDataImport" [ val file [ concat  [get-user-dir] "/rcptt/Universal_patienten.xls" ] ] 
	[ val tab [ t tab_data_kontakte ] ] {
	concat "ReadDataImport tab " $tab " from " $file " exists? " [ get-file $file | get exists ] | log
	get-file $file| get exists | verify-true
	get-menu -path [ t menu_data_import ] | click
	get-window [ t view_general_data_import ] | get-tab-folder | get-tab-item $tab | click
	set-dialog-result File $file
	with [get-window [ t view_general_data_import  ] ] {
	    get-button [ t btn_dataimport_select_file ] | click	    	
  	    get-eclipse-window | get-object | save-screenshot  [concat $imagesDir "ReadDataImport.png"]

	    get-button [ t ok ] | click
    }
}
// Ende
//
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

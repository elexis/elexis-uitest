--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Importers
Element-Type: context
Element-Version: 2.0
Id: _7pLWIBQVEemEb8FG0wciYA
Runtime-Version: 2.4.1.201903190000
Save-Time: 4/1/19 10:55 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

// When running Jenkis-CI we often had the problem that
// Clicking Leistungen öffnen did not work
// Therefore we open the Codes/Codes view

proc "OpenLeistungen" {
	// this makes problem get-button [ t btn_services ] | click
	open-window-by-name "Codes"
	with [get-view Codes] {
		 get-tab-folder | key-type "M1+m"
	}
}

proc "SetzeSpracheBeiImportern" [ val language "deutsch" ] {
	get-preferences-menu | click
	with [get-window [t preferences ] ] {
		get-tree | select [ t System_Allgemein ]
		get-button $language | click
		get-button [ t apply_and_close] | click
	}
}

// We try to increase here some timeout to let artikelstamm and tarmed importer complete successfully
//	set-q7-option uijobHangTimeout	   30000
//	set-q7-option jobSleepingStepTimeout 120000
// https://www.eclipse.org/forums/index.php/m/1734168/?srch=wait+progress+view#msg_1734168
// with 1000 and 2000 the OK danach arrived after 2 seconds
// 21:32:16.991 [Worker-28] INFO  PLATFORM - [org.eclipse.core.runtime] ReadArtikelstamm OK before q7
// 21:32:18.135 [Worker-22] INFO  PLATFORM - [org.eclipse.core.runtime] ReadArtikelstamm OK danach

// Achtung: Nur für demoDB gedacht und dort läuft es auch nicht wie geplant,
// da die Anpassungen beim Import immer noch auf dem alten Wert steht
proc "SetArtikelstammOrigin2oddb2ml" {
	if [ get-file $sqlCmdFile | get exists ] {
		delete-file $sqlCmdFile
		concat "deleted: " $sqlCmdFile | log
	}
	str "UPDATE ARTIKELSTAMM_CH set  ADDDSCR= 'oddb2ml' where ID like 'VERSION' and ADDDSCR = 'medindex'; ---" | write-lines $sqlCmdFile
	try {
		exec-process "java" "-classpath" $h2Jar "org.h2.tools.RunScript" "-url" $jdbcConnection "-user" "sa" "-script" $sqlCmdFile
	} -catch {
		log "Some error while deleting ARTIKELSTAMM_CH"
	}
	concat "DeleteAllArtikelstammItems done via: " $sqlCmdFile | log
}

proc "ExecuteDataImport" [ val file ] {
	concat "ExecuteDataImport file "  $file " exists? " [get-file $file| get exists] | log
	get-file $file| get exists | verify-true
	with [get-window [ t view_data_import ]] {
		get-button [ t btn_data_import_search ] | click
		get-button OK | click
	}
	concat "ExecuteDataImport with "  $file " done" | log
	elexisLogMayNotContain "java.lang.OutOfMemoryError"
}

proc "ReadArtikelstamm" [ val file [ concat  [get-user-dir] "/rcptt/Artikelstamm_Short_v5.xml" ] ] {
	open-window-by-name [ t daten_artikel ]
	concat "before ReadArtikelstamm file: " $file | log
	with [get-view [ t artikel ]] {
		get-tab-folder | get-tab-item Artikelstamm | click
		get-editbox -after [get-label [ t artikelstamm_label  ]] | set-focus
	}
	get-button [ t show_menu ] | click
	get-view [ t artikel ] | get-menu -path [ t btn_import ] | click
	set-dialog-result File $file
	concat "ReadArtikelstamm OK before " $file | log
	with [get-window [ t view_data_import ]] {
		get-button [ t btn_data_import_search ] | click
		// TODO: Wait that pull request 174 for Tickets 14745 gets merged
		// get-button "Pharma-Artikel" | check
		get-button OK | click
	}

	concat "ReadArtikelstamm OK danach " $file | log
	
	get-eclipse-window | gen-screenshot [concat $imagesDir "ArtikelstammImporter_1.png"]
	// 19:25:06.595 [Worker-4] INFO  a.m.c.a.e.c.i.ArtikelstammImporter - ArtikelstammImporterPage.doImport /home/niklaus/elexis/rcptt/Artikelstamm_Short_v5.xml
	// 19:25:15.072 [Worker-4] INFO  a.m.c.a.e.c.i.ArtikelstammImporter - [PI] Leistungsblock validation finished
	get-view [ t artikel ] | get-button [ t btn_automatic_search ] | uncheck
	get-eclipse-window | gen-screenshot [concat $imagesDir "ArtikelstammImporter_2.png"]
	concat "ReadArtikelstamm done: " $file |log
}

proc "ReadBlocksExchange" [ val file  [ concat  [get-user-dir] "/rcptt/blocks_landarztpraxis_sevelen.xchange"] ] {
	get-eclipse-window | gen-screenshot [concat $imagesDir "ReadBlocksExchange_0.png"]
	OpenLeistungen
	concat "ReadBlocksExchange file is: " $file | log
	get-eclipse-window | gen-screenshot [concat $imagesDir "ReadBlocksExchange_1.png"]
	with [get-view Codes] {
		get-tab-folder | get-tab-item [ t blocks ] | click
		get-editbox -after [get-label [ t Name ] ] | set-focus
	}
	get-button [ t show_menu ] | click
	get-view Codes | get-menu -path [ t btn_import ] | click
	set-dialog-result File $file
	ExecuteDataImport $file
	get-eclipse-window | gen-screenshot [concat $imagesDir "ReadBlocksExchange.png"]
}

proc "AssertNoErrorInProgress" {
	open-window-by-name "General/Progress View"
	get-tab-folder | key-type "M1+m"
	with [get-view "Progress View"] {
		global [val errorInImport "false"] -override
		try {
			get-link "Fehler beim Import.*"
			global [val errorInImport "true"] -override
	} -error [ val errorObj ] -catch {
			concat  " While checking for Fehler: error " $errorObj | log
	}
	if [ $errorInImport | matches "true" ] {
			$errorInImport | verify-false
		}
	}
}

proc "ReadAnalysenListe" [ val file [ concat  [get-user-dir] "/rcptt/EAL_01012019.xlsx" ] ] {
	OpenLeistungen
	get-eclipse-window | gen-screenshot [concat $imagesDir "ReadEAL_1.png"]
	get-view Codes | get-tab-folder | get-tab-item "EAL 2009" | click
	concat "ReadAnalysenListe file is: " $file | log
	get-button [ t show_menu ] | click
	get-view Codes | get-menu -path [ t btn_import ] | click
	set-dialog-result File $file
	ExecuteDataImport $file
	// TODO: Warum kommt der Fehler beim Import for Input String "1'000"
	// AssertNoErrorInProgress
}

proc "ReadMigel" [ val file [ concat  [get-user-dir] "/rcptt/MiGeL2019v1.csv" ] ] {
	open-window-by-name [ t daten_artikel ]
	get-eclipse-window | gen-screenshot [concat $imagesDir "ReadMigel_1.png"]
	get-view [ t artikel ] | get-tab-folder | get-tab-item MiGeL | click
	get-view [ t artikel ] | get-menu -path [ t btn_import ] | click
	set-dialog-result File $file
	get-window [ t view_data_import ] | get-button [ t delete_all_data ] | check
	ExecuteDataImport $file
	get-eclipse-window | gen-screenshot [concat $imagesDir "ReadMigel.png"]
}

proc "ReadTarmed" [ val file  [ concat  [get-user-dir] "/rcptt/TARMED__Datenbank_01.09.00_BR_KVG-27.12.2017.mdb" ]  ] {
	OpenLeistungen
	get-eclipse-window | gen-screenshot [concat $imagesDir "ReadTarmed_1.png"]
	with [get-view Codes] {
		get-tab-folder | get-tab-item Tarmed | click
		get-editbox -after [get-label Ziffer] | set-focus
	}
	get-eclipse-window | gen-screenshot [concat $imagesDir "ReadTarmed_2.png"]
	get-button [ t show_menu ] | click
	get-view Codes | get-menu -path [ t btn_import ] | click
	set-dialog-result File $file
	with [get-window [ t view_data_import ]] {
		get-button [ t btn_data_import_search ] | click
		with [get-combo -after [get-label [ t relevant_tarmed_caselaw ] ] ] {
		set-text KVG
		select KVG
		}
		concat "ReadTarmed: Starting with file: " $file | log
		get-button OK | click
		concat "ReadTarmed: Done with file: " $file | log
	}
	concat "Start wait for Import erfolgreich" | log
	loop [val i 0] {
		try {
			get-window [ t Import_erfolgreich ] | gen-screenshot [concat $imagesDir "tarmed_done.png" ]
			get-window [ t Import_erfolgreich ] | get-button OK | click
			concat "ReadTarmed: Wait for Import erfolgreich done lang: " [ safe-get-java-property "user.language"  ] " window: " [ t Import_erfolgreich ] " erfolgreich i is "  $i |  log
		} -error [val errorObj] -catch {
			concat "ReadTarmed: Will wait another 10 seconds for window lang " [ safe-get-java-property "user.language"  ] " window: " [ t Import_erfolgreich ] " erfolgreich i is "  $i | log
			wait 10000 // Wait 10 seconds, but at most 15 minutes
			if [ $i | lt 90 ] { recur [$i | plus 1]
			} -else {
				concat "ReadTarmed waited too long i is " $i | log
				bool false | assert-true
			}
		}
	}
}

proc "ReadDataImport" [ val file [ concat  [get-user-dir] "/rcptt/Universal_patienten.xls" ] ] 
	[ val tab [ t Kontakt_Importer ] ] {
	concat "ReadDataImport tab " $tab " from " $file " exists? " [ get-file $file | get exists ] | log
	get-file $file| get exists | verify-true
	get-menu -path [ t menu_data_import ] | click
	get-window [ t view_general_data_import ] | get-tab-folder | get-tab-item $tab | click
	set-dialog-result File $file
	with [get-window [ t view_general_data_import  ] ] {
		get-button [ t btn_dataimport_select_file ] | click			
  		get-eclipse-window | gen-screenshot  [concat $imagesDir "ReadDataImport.png"]
		get-button [ t ok ] | click
	}
}

proc "ReadComplimentary" [ val file ] {
	OpenLeistungen
	with [get-view Codes] {
	    get-tab-folder | get-tab-item "Komplementärmedizin" | click
		get-eclipse-window | gen-screenshot [concat $imagesDir "ReadComplimentary_starting.png"]
	}
	// get-button "Menü anzeigen" -index 2 | click
	get-view Codes | get-menu -path [ t btn_import ] | click
	set-dialog-result File "/home/niklaus/elexis/rcptt/complementary_190101.csv"
	get-window [ t view_data_import ] | get-button [ t btn_data_import_search ] | click
	get-window [ t view_data_import ] | get-button OK | click
	get-view Codes | get-button [ t btn_execute_search ] | click
	get-eclipse-window | gen-screenshot [concat $imagesDir "ReadComplimentary_done.png"]
}
// Ende
//
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

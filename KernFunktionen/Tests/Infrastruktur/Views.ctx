--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Views
Element-Type: context
Element-Version: 2.0
Id: _P7r6AP-yEeiDKs20GBwclA
Runtime-Version: 2.4.0.201901240010
Save-Time: 1/25/19 9:05 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

global [val viewPathName "" ] -override
log $viewsCsvFile
exec-with-options -command {
	get-menu -path [ t menu_select_view ]| click
	get-window [ t show_view ] | get-tree | expand-all | get-table-data | write-csv-file $viewsCsvFile
	wait 100
	get-window [ t show_view ] | get-button [ t cancel ] | click
}

proc "open-window-by-name" [ val windowName ] {
	get-menu -path [ t menu_select_view ]| click
	with [get-window [ t show_view ]] {
		if [ matches $windowName ".*/.*" ] {
			get-tree | expand-all | select [ replaceAll $windowName  " / " ".*" ] // needed for CST Minima / Maxima
		} -else {
			get-tree | expand-all | select [ concat ".*/" $windowName ]
		}
		get-button [t open ] | click
	}
}

global [val curViewSection ""] -override
proc "check-one-view-item" [val viewName] {
	// viewName as seen in the CSV file

	global [val viewPathName [replaceFirst $viewName "0," ""]] -override
	if [$viewName | str | matches "^0,.*"] {
		global [val curViewSection [replaceFirst $viewName "0," ""]] -override
		// global [val viewPathName [concat $curViewSection ".png"]] -override
		concat "viewName: " $viewName " in curViewSection: " $curViewSection | log
		resetPerspective
		// TODO: View Auwahl anzeigen?
	} -else {
		global [val viewPathName [replaceFirst $viewName "1," ""]] -override
		global [val viewPathName [concat [$curViewSection] "/" $viewPathName]] -override
		// concat "viewPathName: " $viewPathName | log
		global [val curViewImage [ concat $imagesDir
			"views/"
			[ replaceAll $viewPathName "\W" "_" ]
			".png"
			] ] -override
		concat "save-screenshot: " $curViewImage | log
		open-window-by-name $viewPathName
		wait 100 // Give it some time to populate it
		// TODO: Check that Die Ansicht konnte nicht erstellt werden is not visible
		// TODO: Check Table "CSTGROUPS" not found
		get-eclipse-window | gen-screenshot -out [ $curViewImage ]
	}
}

proc "visit-all-views" [ val addToIgnorePattern "" ] {
	read-lines $viewsCsvFile | foreach [val item] {
		concat "visit-all-views checks: " $item " addToIgnorePattern: " $addToIgnorePattern  | log
		// if [$item | str | matches "^_indent.*" [
		 if [$item | str | matches [
			concat  "^_indent.*" $addToIgnorePattern] ] {
			concat "Skipping first line and " $addToIgnorePattern " in visit all Views"| log
		} -else {
			check-one-view-item $item
		}
	}
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

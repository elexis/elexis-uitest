--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Views
Element-Type: context
Element-Version: 2.0
Id: _P7r6AP-yEeiDKs20GBwclA
Save-Time: 4/22/21 11:25 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

global [val viewPathName "" ] -override
global [ val defaultLanguage [ get-java-property "user.language" "de" ] ] -override

proc "WriteViewsCsvFile" {
	concat "WriteViewsCsvFile: " [ t menu_select_view ] " -> " [ t show_view ] | log
	get-menu -path [ t menu_select_view ]| click
	exec-with-options -command {
		get-window [ t show_view ] | get-tree | expand-all | get-table-data | write-csv-file $viewsCsvFile
	}
	wait 100
	get-window [ t show_view ] | get-button [ t cancel ] | click
	log "WriteViewsCsvFile: done"
}

proc "open-window-by-name" [ val windowName ] {
	try {
		get-view $windowName
	} -catch {
		concat "open-window-by-name: lang " $defaultLanguage  ": "[ t menu_select_view ] " for " [ t windowName ]| log
		if [ concat "" $defaultLanguage "" | eq "fr" ] {
			get-eclipse-window | key-type "M1+n" // Ctrl-N
		} -else {
			set-dialog-result File [ concat "/dummy_" $windowName ]
			get-menu -path [ t menu_select_view ]| click
		}
		with [get-window [ t show_view ]] {
			if [ matches $windowName ".*/.*" ] {
				get-tree | expand-all | select [ replaceAll $windowName  " / " ".*" ] // needed for CST Minima / Maxima
			} -else {
				get-tree | expand-all | select [ concat ".*/" $windowName ]
			}
			get-button [t open ] | click
		}
		// Seen when running ImportSwissbase with french Elexis
//			concat "open-window-by-name: " [$errorObj | get message] " for " $windowName | log
	}
}

global [val curViewSection ""] -override
proc "check-one-view-item" [val viewName] {
	// viewName as seen in the CSV file

	global [val viewPathName [replaceFirst $viewName "0," ""]] -override
	if [$viewName | str | matches "^0,.*"] {
		global [val curViewSection [replaceFirst $viewName "0," ""]] -override
		// global [val viewPathName [concat $curViewSection ".png"]] -override
		concat "viewName: " $viewName " in curViewSection: " $curViewSection | log
		resetPerspective
		// TODO: View Auwahl anzeigen?
	} -else {
		global [val viewPathName [replaceFirst $viewName "1," ""]] -override
		global [val viewPathName [concat [$curViewSection] "/" $viewPathName]] -override
		// concat "viewPathName: " $viewPathName | log
		global [val curViewImage [ concat $imagesDir
			"views/"
			[ replaceAll $viewPathName "\W" "_" ]
			".png"
			] ] -override
		open-window-by-name $viewPathName
		wait 100 // Give it some time to populate it
		// TODO: Check that Die Ansicht konnte nicht erstellt werden is not visible
		// TODO: Check Table "CSTGROUPS" not found
		get-eclipse-window | gen-screenshot -out [ $curViewImage ]
	}
}

proc "visit-all-views" [ val addToIgnorePattern "" ] {
	WriteViewsCsvFile
	read-lines $viewsCsvFile | foreach [val item] {
		concat "visit-all-views checks: " $item " addToIgnorePattern: " $addToIgnorePattern  | log
		// if [$item | str | matches "^_indent.*" [
		 if [$item | str | matches [
			concat  "^_indent.*" $addToIgnorePattern] ] {
			concat "Skipping first line and " $addToIgnorePattern " in visit all Views"| log
		} -else {
			check-one-view-item $item
		}
	}
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

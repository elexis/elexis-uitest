--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _-OHP8BOEEemEb8FG0wciYA,_7pLWIBQVEemEb8FG0wciYA
Element-Name: EphaInteraktionen
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _tFxLkBNbEemKBfY3bI9wrQ
Save-Time: 12/19/20 10:58 AM
Testcase-Type: ecl

------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa
Content-Type: text/plain
Entry-Name: .description

Tests für die Epha-Interaktionen, welche neu via folgende Tickets dazugekommen sind:

* https://redmine.medelexis.ch/issues/14645 EPHA-Interaktionen in VerrechnungsDisplay integrieren
* https://redmine.medelexis.ch/issues/14780 EPHA-Interaktionen: Kein Text ausgeben, falls unterdrückt. Verrechnungstext wrappen

Die Benutzerdoku dazu findet man unter https://wiki.elexis.info/Medikamentenliste#Pr.C3.BCfung_auf_Interaktionen_zwischen_Medikamenten

Für die Tests werden die folgenden Medis gebraucht

Paroxetin -> Losartan Keine Massnahmen notwendig
LOSARTAN, METOPROLOL Vorsichtsmassnahmen
Paroxetin -> Metoprolol Kombination vermeiden
Paroxetin -> Tamoxifen Kontraindiziert.


------=_.description-216f885c-d591-38ce-8ea2-e4f8cb4d6ffa--
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

// TODO: Make it work with the demoDB
// TODO: Fix not displayin when we downloaded the interaction for the first time

// First we must setup the environment consisting of
// * small artikelstamm
// * Two patients to be able to switch between
// ** Armes Wesen ohne Fall
// ** Mille Tonnere mit Fall und Kons
// * One of them with an open consultation to be able to add artikelstamm items

proc "ReadArtikelstammIfNecessary" {
	log "ReadArtikelstammIfNecessary started"
	OpenAndResetStartPerspektive
	try {
		open-window-by-name [ t daten_artikel  ]
		with [get-view [ t artikel ] ] {
		    get-tab-folder | get-tab-item Artikelstamm | click
		    with [get-tab-folder] {
		        key-type "M1+m"
		    }
			log "ReadArtikelstammIfNecessary before ArtikelstammExecuteSearch ASPIRIN"
			ArtikelstammExecuteSearch
		    get-table | get-item -path "ASPIRIN S Tabl 500 mg 20 Stk <8.7> " | get-property caption
				| equals "ASPIRIN S Tabl 500 mg 20 Stk <8.7>" | verify-true
		}
		log "ReadArtikelstammIfNecessary no read needed"
	} -error [val errorObj] -catch {
		concat "ReadArtikelstammIfNecessary: Must read Artikelstamm: " [$errorObj | get message] | log
		SetArtikelstammOrigin2oddb2ml
		ReadArtikelstamm
	}
}

proc "PrepareEphaCheck" {
	OpenAndResetStartPerspektive
	SetEphaInteractionCheck true
	if [SelectPatient Armes Wesen | eq false] {
		PatientenErstellen  Armes Wesene true "30.3.1979" "Nebenstrasse 33" 3320 Elexiskon "055 323 45 67"
	}

	if [SelectPatient Mille Tonnere| eq false] {
		PatientenErstellen  Mille Tonnere true "30.3.1979" "Nebenstrasse 33" 3320 Elexiskon "055 323 45 67"
	}
	if [FallOffen [ t case_UVG ] "201."] {
		log "Offenen Fall UVG gefunden"
	} -else {
		FallZuPatientErstellen [ t case_UVG ] -caseReason [ t reason_UVG ] -startDate  "20.08.2018"
		log "Fall UVG 20.08.2018 erstellt"
	}

	SelectFall  [ t case_UVG ] "201"
	if [ HasKonsSelected | eq false] {
		kons-erstellen "BasisTests für Mille Tonnere mit UVG"
	} -else {
		log "HasKonsSelected returned true"
		AllVerrechnungenEntfernen
	}
}

proc "EnsurePatientChanged" {
	// The change is only visible after closing the view or forcing a refresh by changing the patient
	SelectPatient Armes Wesen -mustExist true
	SelectPatient Mille Tonnere -mustExist true
}

proc "CheckEphaInKons" {
	resetPerspective
	log "CheckEphaInKons kons-add-artikelstamm Paroxetin"
	kons-add-artikelstamm Paroxetin "Paroxetin.*20 mg 14 Stk.*"
	VerifyInteractionStatus [ t interaction_no_relevant ]
	get-view [ t kons_view ] | get-control Any -after [get-label [ t btn_diagnosis ]] -index 8 |
		gen-screenshot [concat $imagesDir "EphaInteractions/no_interaction.png"]

	log "CheckEphaInKons kons-add-artikelstamm LOSARTAN"
	kons-add-artikelstamm LOSARTAN $Losartan
	EnsurePatientChanged // Is needed if it is the first time we downloaded the interaction. TODO: FIX IT!
	VerifyInteractionStatus [ t interaction_no_relevant ]
	get-view [ t kons_view ] | get-control Any -after [get-label [ t btn_diagnosis ]] -index 8 |
		gen-screenshot [concat $imagesDir "EphaInteractions/probably_no_interaction.png"]

	log "CheckEphaInKons kons-add-artikelstamm Metoprolol"
	kons-add-artikelstamm METOPROLOL "Metoprolol.*100 Stk.*"
	VerifyInteractionStatus [ t interaction_periodic_control ]
	VerifyInteractionLink
	get-view [ t kons_view ] | get-control Any -after [get-label [ t btn_diagnosis ]] -index 8 |
		gen-screenshot [concat $imagesDir "EphaInteractions/check_interaction.png"]

	log "CheckEphaInKons kons-add-artikelstamm Nolvadex"
	kons-add-artikelstamm Nolvadex "Nolvadex.*100 Stk.*"
	VerifyInteractionStatus [ t interaction_contra_indicated ]
	VerifyInteractionLink
	get-view [ t kons_view ] | get-control Any -after [get-label [ t btn_diagnosis ]] -index 8 |
		gen-screenshot [concat $imagesDir "EphaInteractions/contra_indicated.png"]

	SetEphaInteractionCheck false
	EnsurePatientChanged
	VerifyInteractionStatus ""
	get-view [ t kons_view ] | get-control Any -after [get-label [ t btn_diagnosis ]] -index 8 |
		gen-screenshot [concat $imagesDir "EphaInteractions/interaction_check_disabled.png"]

	SetEphaInteractionCheck true
	EnsurePatientChanged
	VerifyInteractionStatus [ t interaction_contra_indicated ]
	VerifyInteractionLink
}

ReadArtikelstammIfNecessary
PrepareEphaCheck
CheckEphaInKons

proc "CheckEphaInMediListe" {
get-view Medikationsliste | get-button "Medikation hinzufügen" | click
with [get-view Leistungen | get-group Alle] {
    get-editbox -after [get-label "Artikel oder Wirkstoff"] | set-text LOSART
    get-table | select $Losartan | select $Losartan
        | select $Losartan | select $Losartan
        | select $Losartan | double-click
}
with [get-window -class PrescriptionSignatureTitleAreaDialog] {
    get-button "Als standard Signatur hinterlegen." | check
    with [get-editbox] {
        set-text 1
        key-type "TRAVERSE_TAB_NEXT"
    }
    with [get-editbox -after [get-label "-"]] {
        set-text 2
        key-type "TRAVERSE_TAB_NEXT"
    }
    with [get-editbox -after [get-label "-" -after [get-label "-"]]] {
        set-text "0.5"
        key-type "TRAVERSE_TAB_NEXT"
    }
    get-editbox -after [get-label "-" -after [get-label "-" -after [get-label "-"]]] -index 1
        | set-text "Langsam auf der Zunge vergehen"
    get-button Fix | click
    get-button Abgabe | click
    get-button OK | click
}
}
// ENDE




------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--

--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Benutzer
Element-Type: context
Element-Version: 2.0
Id: _TrPRoAOnEemXxf0UDvXYOw
Runtime-Version: 2.4.0.201901240010
Save-Time: 2/11/19, 5:20 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc EnterOneDetail [ val key ] [ val idMandant 1 ]  {
	concat EnterOneDetail $key| log
	concat "EnterOneDetail: " $key " translation "  [ t $key ] | log
	concat "EnterOneDetail: " $key " praxiskey "  "praxis.mandant.1" $key | log
	concat "EnterOneDetail: " $key " GetPraxisProperty "  [ GetPraxisProperty [ concat "praxis.mandant." $idMandant "." $key ] ] | log
	with [get-window Identifikationselemente] {
        with [get-group "Bitte geben Sie die notwendigen Identifikationsdaten ein"] {
			 let [ val label [ t $key ] ] {
		         let [ val value [ GetPraxisProperty [ concat "praxis.mandant." $idMandant "." $key ] ] ] {
					concat "EnterOneDetail: " $key " label "  $label " value " $value | log
					get-editbox -after [get-label $label] | set-text $value
				}
			}
		}
	}
}

proc EnterOneFinanzDetail [ val key ] [ val idMandant 1 ]  {
	concat EnterOneFinanzDetail " key is: " "praxis.mandant." $idMandant ".bank." $key | log
	let [ val value [ GetPraxisProperty [ concat "praxis.mandant." $idMandant ".bank." $key ] ] ] {
		concat "EnterOneFinanzDetail: " $key " value " $value | log
		with [get-editbox -after [get-label $key ] ] {
		   set-text  $value
	   }
   }
}

proc SetMandantenDetails [ val idMandant 1 ] {
	get-preferences-menu | click
	with [get-window Benutzervorgaben] {
	    get-tree | select "Gruppen und Rechte/Benutzerverwaltung"
		GetPraxisProperty [ concat "praxis.mandant." $idMandant ".kuerzel"]  | log
		try {
	    	get-table | select  [ GetPraxisProperty [ concat "praxis.mandant." $idMandant ".kuerzel"] ] | double-click
	        get-button Abbrechen | click
    	} -catch {
    		log "Mandant not found. Must create it"
    		get-button Abbrechen | click
    		create-user -userName [ GetPraxisProperty [ concat "praxis.mandant." $idMandant ".kuerzel"] ] -role executive_doctor
    		  -plainPW [ GetPraxisProperty [ concat "praxis.mandant." $idMandant ".pw"] ] -forMandat "" -kontakt ""
    	}
	}
  	get-preferences-menu | click
	with [get-window Benutzervorgaben] {
	    get-tree | select "Abrechnungssysteme/Tarmed-Rechnungen"
		get-group Detailangaben | get-button "Bills electronically" | check
	    get-group Detailangaben | get-link Leistungserbringer | click
	    with [get-window Identifikationselemente] {
		    str "tarmed_anrede tarmed_ean tarmed_ksk tarmed_kanton1 tarmed_kanton2 tarmed_rolle tarmed_nif tarmed_diag_system tarmed_esr5oresr9 tarmed_esrplus tarmed_erbringungsort tarmed_speziality tarmed_tg_or_tg"  |
		    split -sep " " | foreach [val item]  {
				concat "item: " $item | log
				EnterOneDetail -key $item -idMandant $idMandant
			}
			get-eclipse-window | get-object | save-screenshot [concat $imagesDir "tarmed_mandant_" $idMandant "_details.png"]
	        get-button OK | click
	    }
	    with [get-group Trustcenter] {
	        get-combo | select "TC test"
	        get-button "eDA Vertrag mit Trustcenter vorhanden. (Rechnungen an TC und Token auf Papier-Rn)" | check
	    }
	    
	    if [ GetPraxisProperty [ concat "praxis.mandant." $idMandant ".pc_konto"] ] {
	    	log "PC-Konto definiert"
		    with [get-group Detailangaben] |  get-link Postkonto | click
		    with [get-window Postkonto] {
		        // get-group "Bitte geben Sie die notwendigen Identifikationsdaten ein" | get-editbox 
		        get-editbox -after [get-label TarmedESRParticipantNumber] | set-text  [ GetPraxisProperty [ concat "praxis.mandant." $idMandant ".pc_konto"] ]
		        get-button OK | click
		    }
	    } -else {
		    get-group Detailangaben | get-link Bankverbindung | click
		    with [get-window "Zahlungsinstitut auswählen"] {
		        get-link Finanzinstitut | click
		        with [get-window "Kontakt auswählen"] {
		        	GetPraxisProperty [ concat "praxis.mandant." $idMandant ".bank.Name" ] | log
		        	get-editbox -index 1 | set-text  [ GetPraxisProperty [ concat "praxis.mandant." $idMandant ".bank.Name" ] ]
		            get-table | select ".+"
		            get-button OK | click
		        }
		        with [get-group "Bitte geben Sie die notwendigen Identifikationsdaten ein"] {
	                EnterOneFinanzDetail Abteilung
	                EnterOneFinanzDetail Postfach
	                EnterOneFinanzDetail TarmedESRParticipantNumber
	                EnterOneFinanzDetail TarmedESRIdentity
					get-editbox -after [get-label TarmedESRParticipantNumber] | get-property text -raw | log
		        }
		        get-button OK | click
	        }
	        // TODO: Why is the work-around necessary in RCPTT???
			get-group Detailangaben | get-link Bankverbindung | click
		    with [get-window "Zahlungsinstitut auswählen"] {
				with [get-group "Bitte geben Sie die notwendigen Identifikationsdaten ein"] {
	                EnterOneFinanzDetail Abteilung
	                EnterOneFinanzDetail Postfach
	                EnterOneFinanzDetail TarmedESRParticipantNumber
	                EnterOneFinanzDetail TarmedESRIdentity
					get-editbox -after [get-label TarmedESRParticipantNumber] | get-property text -raw | log
		        }
		        get-button OK | click
		    }
		    get-button Anwenden | click
	    }
		// Verify TarmedESRParticipantNumber
	    if [ GetPraxisProperty [ concat "praxis.mandant." $idMandant ".pc_konto"] ] {
			log "Skip verify pc_konto"
	    } -else {
			log "Verify Bankverbindung"
		    get-group Detailangaben | get-link Bankverbindung | click
			get-window Benutzervorgaben | get-window "Zahlungsinstitut auswählen"
			    | get-group "Bitte geben Sie die notwendigen Identifikationsdaten ein" | get-editbox
			    -after [get-label TarmedESRParticipantNumber] | get-property text -raw | log
			get-window Benutzervorgaben | get-window "Zahlungsinstitut auswählen"| get-object | save-screenshot [concat $imagesDir "tarmed_mandant_" $idMandant "_bank.png"]
			get-window Benutzervorgaben | get-window "Zahlungsinstitut auswählen" | get-button OK | click
	    }
		get-eclipse-window | get-object | save-screenshot [concat $imagesDir "tarmed_mandant_" $idMandant ".png"]
	    get-button "Anwenden und Schließen" | click
	}
}

proc "create-user" [val userName] [val forMandat] [val role] [val kontakt]  [ val plainPW "test" ]{
	get-preferences-menu | click
	get-window [ t preferences ] | get-tree | expand-all | select [ t prefs_users_and_rights ]
	try {
		get-window Benutzervorgaben | get-table | get-item -path $userName | get-property caption | equals $userName | verify-true
		concat "Benutzer, " $userName " already exists" | log
		get-window Benutzervorgaben | get-button [ t cancel ] | click
	} -error  [val errorObj]  -catch {
		// concat "EError was: " $errorObj | log
    	concat "create-user " $userName " for " $forMandat " role " $role " kontakt " $kontakt " with password " $plainPW | log
		with [get-window [ t preferences ]] {
		    get-button -after [get-label [t btn_user_admin] ] -index 3 | click
		}
		with [get-window [ t dlg_create_user ] ] {
		    get-editbox 
		        -after [get-label [ t dlg_create_user_set] ] | set-text $userName
		    get-button [ t ok ] | click
		}
		with [get-window [ t preferences ]] {
		    get-table | select $userName
		    with [get-group [t dlg_create_user_accounting] ] {
		        get-group [t user_role] | get-table | get-item -path $role | check
		        if [ $forMandat | matches ".+" ] {
		        	get-group [t select_works_for] | get-table | get-item  -path $forMandat | check
		    		get-window [t view_no_contact] | get-button [t ok] | click
		        }
		    }
	        if [ $kontakt | matches ".+" ] {
			    get-group [t dlg_create_user_accounting] | get-link [ t dlg_create_user_accounting_change ] | click-link
			    with [get-window [t select_contact] ] {
			        get-table | select  $kontakt
			        get-button [ t ok ] | click
			    }
		    }
		    with [get-group Systemzugang] {
		        get-button Administrator | check
		        get-link "<a>Passwort ändern</a>" | click-link
		    }
		    concat "Neues Passwort ist: " $plainPW| log
		    with [get-window -class ChangePasswordDialog] {
		        get-editbox -after [get-label "Neues Passwort"] | set-text $plainPW
		        get-editbox -after [get-label "Bestätigen"] | set-text $plainPW
		        get-button "Prüfregeln ignorieren (nicht empfohlen)" | check
		        get-button OK | click
		    }
			get-eclipse-window | get-object | save-screenshot [concat $imagesDir VerantwortlicherArzt "_created.png" ]
			get-button [ t apply_and_close ] | click
		}
		concat "Benutzer " $userName " angelegt. verantwortlich" $forMandat " rolle " $role " kontakt " $kontakt | log
	}
}
// Ende
//

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

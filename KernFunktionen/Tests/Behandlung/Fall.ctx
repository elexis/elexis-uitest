--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Fall
Element-Type: context
Element-Version: 2.0
Id: _p5Ui8P79EeiFyvHqIglmCA
Runtime-Version: 2.4.1.201903190000
Save-Time: 4/12/19 5:31 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc "set-date-picker-date" [val window ] [ val labelBefore -input ] [ val "date" ] {
	concat "set-date-picker-date: " window ": " $labelBefore ": " $date| log
	global [ val myCombo [  get-window $window | get-editbox -after [get-label $labelBefore ] ]] -override
	get-window $window | get-editbox -after [get-label $labelBefore ] | log
	get-window $window | get-editbox -after [get-label $labelBefore ] | set-text $date
	get-window $window | get-editbox -after [get-label $labelBefore ] | key-type Enter
}

proc "SetRightToDeleteCase" {
	get-preferences-menu | click
	with [get-window [ t preferences ] ] {
		get-tree | select [ t select_roles_right ]
		get-tree -after [get-link [t dlg_create_user_accounting_change ] ] | get-item -path  [ t right_delete_case ]
		  | get-property "values['u']" -raw | log
		if [ get-tree -after [get-link [t dlg_create_user_accounting_change ] ] | get-item -path  [ t right_delete_case ]
		  | get-property "values['u']" -raw | equals x | eq true ] {
			log "User has already the right"
		  } -else {
			log "Must git the user the right"
			get-tree -after [get-link [t dlg_create_user_accounting_change ] ] | select [ t right_delete_case ] | check
		  }
		get-button [ t apply_and_close ] | click
	}
}

proc DeleteSelectedCase {
	get-view [ t view_cases ] | click
	get-view [ t view_cases ] | get-table | get-menu -path  [ t btn_delete_case ] | click
}

proc FallZuPatientErstellenFallsNichtVorhanden [val caseLaw [t case_UVG ] ] [ val caseReason [ t reason_UVG ]  ] [val startDate "201.*"] [val endDate "01.08.2018"] {
	if [FallOffen -caseLaw $caseLaw -startDate $startDate ] {
		concat  "Offenen Fall " $caseLaw " gefunden für Startatum " $startDate
	} -else {
		FallZuPatientErstellen -caseLaw $caseLaw -caseReason $caseReason -startDate "20.04.2018"
	}
}
proc FallZuPatientErstellen [val caseLaw [t case_UVG ] ] [ val caseReason [ t reason_UVG ]  ] [val startDate ""] [val endDate ""] {
	open-window-by-name [ t view_cases ]
	get-view [ t view_cases ]| get-button [ t btn_create_new_case ] | click
	wait 100
	
	with [get-window [ t dlg_new_case ] ] {
		concat "caseLaw: " $caseLaw  " caseReason: " $caseReason | log
		get-combo -after [get-label [ t dlg_new_case_caseMethod ] ] | select $caseLaw
		get-combo -after [get-label [ t dlg_new_case_caseReason ] ] | select $caseReason
		if [ $startDate | eq "" ] {
			log "Skip setting startDate as empty"
		} -else {
			set-date-picker-date [ t dlg_new_case ] [ t dlg_new_case_startDate ] $startDate
		}
		if [ $endDate | eq "" ] {
			log "Skip setting endDate as empty"
		} -else {
			set-date-picker-date [ t dlg_new_case ] [ t dlg_new_case_endDate ] $endDate
		}
		concat $imagesDir "Fall_" $caseLaw "_" $startDate "_created.png" | log
		get-eclipse-window | gen-screenshot [concat $imagesDir "Fall_" $caseLaw "_" $startDate "_created.png"]
		get-button  [ t ok] | click
	}
}

proc SelectFall [val caseLaw] [val startDate] {
	let [ val searchPattern [ concat ".*" $caseLaw ".*" $startDate ".*"] ] {
		try {
			get-view [ t view_cases ] | get-table | select $searchPattern | click
		} -catch {
			// TODO: Why is this workaround necessary?
			get-view [ t view_cases ] | close
			open-window-by-name [ t view_cases ]
			get-view [ t view_cases ] | get-table | select $searchPattern | click
		}
	}
}

proc FallOffen [val caseLaw] [val startDate] {
	let [val searchPattern [concat ".*" $caseLaw ".*"]] {
		try {
			concat "MySearchpattern is: " $searchPattern | log
			get-view [ t view_cases ] | get-table | get-item $searchPattern | get-property caption
			get-view [ t view_cases ] | get-table | get-item $searchPattern | select-item
			bool true  // 0 stands for true
		} -catch {
			bool false // 1 stands for false
		}
	}
}

proc AktuellenFallVerrechnen {
	let [val nrItemsBefore [ GetNrOfInvoices -status [ t Offen ] ] ] {
		concat "AktuellenFallVerrechnen: before " $nrItemsBefore " items"| log
		get-view [ t view_cases ] | get-table | get-menu -path [ t btn_create_invoice ] | click
		try {
			get-window [ t error ] | get-label -index 1 |  get-property caption -raw | log
			log "AktuellenFallVerrechnen: Failed. See message above" -severity error
			close-window-if-open [ t error ]
		} -catch {
			log "Catched error"
		}
		let [val nrItemsAfter [ GetNrOfInvoices -status [ t Offen ] ] ] {
			concat "AktuellenFallVerrechnen: after have " $nrItemsAfter " items"| log
			$nrItemsBefore | plus 1 | equals $nrItemsAfter | assert-true -message
				[ concat "AktuellenFallVerrechnen: After invoicing expected "  [$nrItemsBefore | plus 1 ] " items but found " $nrItemsAfter]
		}
	}
	// get-window [ t error ] | verify-false
}

global [val auf_id 1 ] -override
proc "FallErzeugeAUF" [ val percentage 100 ] [ val reason "RCPTT Test-Grund" ] [ val comment "" ] [ val startDate "" ] [ val endDate ""]  {
	concat "FallErzeugeAUF. Warum ist hier manchmal kein Fall ausgewählt? " $auf_id | log
	get-eclipse-window | gen-screenshot [concat $imagesDir "AUF-" $auf_id "-before-create.png"]
	with [get-view [ t AUF ]] {
		click
		get-button [ t btn_create_new_auf ] | click
	}
	with [get-window [ t Arbeitsunfaehigkeit ] ] {
		with [get-editbox -after [get-label [ t Prozent ]]] {
			set-text $percentage
		}
		with [get-editbox -after [get-label [ t Grund ]] -index 1] {
			set-text $reason
		}
		get-editbox -after [get-label [ t Zusaetzliche_Angaben ] ] | set-text $comment
		get-eclipse-window | gen-screenshot [concat $imagesDir "AUF-" $auf_id ".png"]
		global [val auf_id [  $auf_id | plus 1 ] ] -override
		get-button OK | click
	}
}
proc "ErzeugeBriefZuAUT" [ val reason ".+" ] [ val adrName "Aquilana" ] [ val adrPostalcode "5401" ] {
	with [get-view [ t AUF ]] {
		// Select first matching
		get-table | select [ concat ".*" $reason ".*" ]
		get-button [ t open_window_for_AUF ] | click
	}
	with [get-window [ t select_contact ]] {
		get-table | select  [ concat ".*" $adrName ".*"  $adrPostalcode ".*" ]
		get-button OK | click
	}
}

proc "FallSetVersicherungsNummer" [ val nummer 123456 ] {
	get-view [ t view_cases ] | get-table | select ".*" | double-click
	try {
		// This works in french wher Cas is name for view Fälle (les cas) und Fall (le cas)
		get-view [ t Fall ] -index 1 | get-button [ t Kopie_an_Patienten ] | check
		 // TODO: French problem: INFO  ch.elexis.data.PersistentObject - field is not mapped Désignation
		get-view [ t Fall ] -index 1 | get-editbox -after [get-label [ t Versicherungsnummer -language de ] ] | set-text $nummer
	} -catch {
		// This work in german
		get-view [ t Fall ] | get-button [ t Kopie_an_Patienten ] | check
		get-view [ t Fall ] | get-editbox -after [get-label [ t Versicherungsnummer -language de ] ] | set-text $nummer
	}
}

// End
//
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Patienten
Element-Type: context
Element-Version: 2.0
Id: _dgccYP7qEeiuqdAcgkHVvQ
Save-Time: 12/19/20 11:00 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc PatientMustBeSelected [val familyName] [val firstName] {
	let [val currentTitle [ get-eclipse-window | get-property title -raw ]]
	    [val pattern  [ concat ".*" $familyName ".*" $firstName ".*" ]] {
		$currentTitle | matches $pattern |
			assert-true -message [ concat "PatientMustBeSelected " $currentTitle " does not match pattern " $pattern]
		concat "PatientMustBeSelected " $currentTitle " does match pattern " $pattern " in window title" | log
	}
}

proc SelectPatient [val familyName] [val firstName] [val mustExist false] {
	open-window-by-name [t view_patients ]
	get-view [t view_patients ] | get-editbox | set-focus
	with [get-view [t view_patients ]] {
	    get-link | click // Clicks on the red "x" to remove all information for name, firstname and date of birth
		with [get-editbox] {
			set-text $familyName
			key-type TRAVERSE_TAB_NEXT
		}
		get-editbox -index 1 | set-text $firstName
		key-type TRAVERSE_TAB_NEXT
		get-view [t view_patients ] | get-table | get-property itemCount | log
	}
	if [ $mustExist ] {
		try { let [ val pattern  [ concat $familyName ".*" $firstName ".*" ]] {
			get-view [t view_patients ]  | get-table | select $pattern
		}} -catch {
			bool false | assert-true -message  [ concat "SelectPatient: needed " $pattern " not found "]
		}
		PatientMustBeSelected $familyName $firstName
	}
	concat "SelectPatient: " $familyName " " $firstName " found " [
		get-view [t view_patients ] | get-table | get-property itemCount -raw ] " times" | log
	get-view [t view_patients ] | get-table | get-property itemCount -raw | int | gt 0
}

/**
 * Set the AHV number for the currently selected patient
 */
proc SetAHVnumber [ val AHVnumber "756.1234.5678.1" ] {
	open-window-by-name [ t Patientendetails_new ]
	get-view [ t Patientendetails ] | get-label [ t Patientendetails_AHV ] | click
	with [get-window [ t Identifikationselemente ] ] {
	    with [get-group [ t notwendigen_Identifikationsdaten ] | get-editbox -after [get-label [ t AHV]]] {
	        set-text $AHVnumber
	    }
		get-eclipse-window | gen-screenshot [concat $imagesDir "AHV_" $AHVnumber ".png"]
	    get-button [ t ok ] | click
    }
}

proc PatientenErstellenFallsNichtVorhanden [val familyName "Wesen" ] [val firstName "Armes" ] [val male [bool true] ] 
	[val birthDate "29.2.1980" ] [val street "Schattenloch" ] [val zipCode "9876" ]
	[val location "Hintertal" ] [val phone "079 12 34 56 78" ] [ val ahv  "756.1234.0000.1" ] {
	if [SelectPatient -familyName $familyName -firstName $firstName | eq false] {
		PatientenErstellen -familyName $familyName -firstName $firstName -male $male -birthDate $birthDate 
			-street $street -zipCode $zipCode -location $location -phone $phone
	}
}

proc PatientenErstellen [val familyName "Wesen" ] [val firstName "Armes" ] [val male true] 
	[val birthDate "29.02.1980" ] [val street "Schattenloch" ] [val zipCode "9876" ]
	[val location "Hintertal" ] [val phone "079 12 34 56 78" ] [ val ahv  "756.1234.9999.1" ] {
	open-window-by-name [t view_patients ]
	get-view [t view_patients ] | get-editbox | set-focus
	get-view [t view_patients ] | get-button [ t btn_create_new_patient ]  | click
	with [get-window [t dlg_new_patient ]] {
		with [get-editbox -after [get-label [ t dlg_new_patient_nom ]]] {
			set-text $familyName
		}
		with [get-editbox -after [get-label [ t dlg_new_patient_firstname ]]] {
			set-text $firstName
		}
		if [$male] {
			get-combo -after [get-label [ t dlg_new_patient_sex ]] | select [ t dlg_new_patient_male ]
		} -else {
			get-combo -after [get-label [ t dlg_new_patient_sex ]] | select [ t dlg_new_patient_female ]
		}
		with [get-editbox -after [get-label [ t dlg_new_patient_birthday ]]] {
			set-text $birthDate
		}
		with [get-editbox -after [get-label [ t dlg_new_patient_street ]]] {
			set-text $street
		}
		with [get-editbox -after [get-label [ t dlg_new_patient_plz ]]] {
			set-text $zipCode
		}
		with [get-editbox -after [get-label [ t dlg_new_patient_location ]]] {
			set-text $location
		}
		get-editbox -after [get-label [ t dlg_new_patient_phone ] ] | set-text $phone
		get-eclipse-window | gen-screenshot [concat $imagesDir $familyName "_"n $firstName "_created.png"]
		get-button [t ok ] | click
		SetAHVnumber -AHVnumber $ahv
	}
	global [ val PatientenErstellenOkay true ] -override
	try {
		get-window "Unplausible Angaben" | get-button [t ok ] | click
		get-window [t dlg_new_patient ] | get-button [t cancel ] | click
		log "PatientenErstellen: Unplausible Angaben gefunden!"
		// TODO: Raise an error if the female could not be created (French)
		global [ val PatientenErstellenOkay false ] -override
	} -catch {
		// Nothing to do
	}
	concat "Res is " $PatientenErstellenOkay | log
	$PatientenErstellenOkay | assert-true
}
// Ende
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

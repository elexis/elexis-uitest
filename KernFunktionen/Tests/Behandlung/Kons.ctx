--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Kons
Element-Type: context
Element-Version: 2.0
Id: _yO1KQAYtEemcp9WoDsgIvA
Runtime-Version: 2.4.1.201903190000
Save-Time: 4/1/19 3:21 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc "KonsErstellenFallsNichtVorhanden" [ val text2enter "NixZuTUn" ] {
	if [ HasKonsSelected | eq false] {
		kons-erstellen -text2enter $text2enter
	} -else {
		log "KonsErstellenFallsNichtVorhanden: HasKonsSelected returned true"
	}
}
proc "kons-erstellen" [ val text2enter "NixZuTUn" ] {
	get-eclipse-window | gen-screenshot [concat $imagesDir "Before_kons_Erstellen.png"]
	t kons_create_new | log
	get-view [ t kons_view ] | get-button [ t kons_create_new ] | click
	try {
		get-view [ t kons_view ]  |get-editbox -after [get-label [ t kons_label_today ] ] | type-text $text2enter
	} -catch {
		log "Workaround: new kons not displayed"
	// TODO: Force closing then opening Konsultation necessary?
	// TODO: Is this an RCPTT problem??
		get-eclipse-window | gen-screenshot [concat $imagesDir "ERROR_must_i_close_the_kons.png" ]
		get-view [ t kons_view ] | close
		open-window-by-name  [ t kons_view ]
		get-view [ t kons_view ]  |get-editbox -after [get-label [ t kons_label_today ] ] | type-text $text2enter
		get-eclipse-window | gen-screenshot [concat $imagesDir "ERROR_reopened_kons.png" ]
	}
}

proc "HasKonsSelected" {
	try {
		// TODO: Force closing then opening Konsultation necessary?
		// TODO: Is this an RCPTT problem??
		get-view [ t kons_view ] | close
		open-window-by-name  [ t kons_view ]
		get-view [ t kons_view ]  | get-label -index 0 | get-property caption -raw | log
		get-view [ t kons_view ]  | get-link "\(.*\)" | get-property caption -raw | log
		get-view [ t kons_view ]  | get-label -index 0 | get-property caption | equals  [ t lbl_no_kons_selected ] | verify-false
		get-view [ t kons_view ]  | get-link "\(.*\)" | get-property caption  | equals "\(.*\)" | verify-true
		bool true
	} -error [val errorObj] -catch {
		log [$errorObj | get message]
		bool false
	}   
}
	

proc "PressAddDiagnosisToKons" {
	// TODO: This button should get a toolbar
	with [get-view  [ t kons_view ] | get-button -after [ get-label [ t btn_diagnosis ] ] ] {
		click
	}
}

proc "PressAddLeistungToKons" {
	// TODO: This button should get a toolbar
	try {
		with [get-view [ t kons_view ] | get-button -after [ get-label [ t btn_accounting ] ] ] {
			click
		}
	} -error [val errorObj] -catch {
		concat "PressAddLeistungToKons failed: " $errorObj | log
		close-window-if-open "Warnung.*"
	}
}

proc "ActivateVisibleKonsView" {
	concat "ActivateVisibleKonsView " [ t kons_view ] | log
	try {
		get-view [ t kons_view ] | get-editbox | set-focus
		log "ActivateVisibleKonsView no problems"
		bool true
	}  -error [val errorObj]  -catch {
		concat "ActivateVisibleKonsView " [ t kons_view ]  " has problems." $errorObj " Trying to close warning" | log
		close-window-if-open "Warnung.*"
		get-view [ t kons_view ] | get-editbox | set-focus
		bool false
	}
}

proc "kons-neue-diagnose" [ val reiter ] [ val item ] {
	ActivateVisibleKonsView
	PressAddDiagnosisToKons
	with [get-view [ t diagnosen ] ] {
		get-group [ t alle_leistungen ] | get-editbox | set-focus
		get-tab-folder | get-tab-item $reiter | click
		with [get-group [ t alle_leistungen ]] {
		get-editbox | set-focus
		with [get-tree] {
			select $item
			get-item -path $item | double-click
			get-item -path $item | click
		}
		}
	}

	// Close Leistungen
	ActivateVisibleKonsView 
	concat "kons-neue-diagnose added " $reiter " " $item | log
}

/**
 * Das geht eine Ewigkeit, weil rcptt sich den ganzen Artikelstamm ins Memory lädt!!
 * <br>Der Name muss so gewählt werden, dass kein hellblau eingefärbter Artikel als erster in der
 * Auswahl erscheint, da diese nicht verrechnet werden können
*/
proc "kons-add-artikelstamm" [ val item_short ] [ val item_long ] [ val expectedItemsAdded 1 ] {
	let [val nrItemsBefore [ kons-get-nr-invoiced-items ] ] {
		concat "kons-add-artikelstamm: " $item_short " have " $nrItemsBefore " items"| log
		PressAddLeistungToKons
		log "PressAddLeistungToKons done"
		with [get-view [t leistungen ] ] {
			get-tab-folder | get-tab-item Artikelstamm | click
			get-group  [ t alle_leistungen ] | get-editbox -after [get-label "Artikel oder Wirkstoff"] | set-focus
			get-group  [ t alle_leistungen ] | get-editbox -after [get-label [ t artikelstamm_label ] ] | set-text $item_short
			log  "before double-click"
			get-button [ t btn_execute_search ] | click
			get-group  [ t alle_leistungen ] | get-table | select $item_long | double-click
			key-type Enter
		}
		concat "kons-neuen-artikelstamm item added " $item_short " " $item_long | log
		ActivateVisibleKonsView
		get-eclipse-window | gen-screenshot [concat $imagesDir "kons-added_" $item_short ".png"]
		let [val nrItemsAfter [ kons-get-nr-invoiced-items ] ] {
			concat "kons-add-artikelstamm: " $item_short " after have " $nrItemsAfter " items"| log
			$nrItemsBefore | plus $expectedItemsAdded | equals $nrItemsAfter | assert-true -message
				[ concat "kons-add-artikelstamm: " $item_short " After invoicing expected "  [$nrItemsBefore | plus $expectedItemsAdded ] " items but found " $nrItemsAfter]
		}
	}
}

proc "kons-add-eigenleistung" [ val itemName  "Tauglichkeitsuntersuchung Führerschein" ] {
	PressAddLeistungToKons
	with [get-view [ t leistungen ] ] {
		get-button [ t btn_show_list ] click
		get-tab-folder | get-tab-item Eigenleistung | click
		with [get-group [ t alle_leistungen ] ] {
		get-editbox -after [get-label Code] | set-focus
		get-table | select $itemName | double-click
		}
	}
	// Close Leistungen
	ActivateVisibleKonsView 
}

/**
 * Select a tarmed by it position. As RCPTT replace a leading 00.0 by 0.0
 * you must enter values like %00.0010 as we will strip all %
 * RCPTT is only to able to select a single item if the full path is given, eg.
 * "00 Grundleistungen \\(KVG\\)/00.01 Allgemeine Grundleistungen \\(KVG\\)/00.01.01 Konsultation, Besuch, Wegentschädigung u.a. \\(KVG\\)/00.0025 \\+ Konsultation bei Kindern unter 6 Jahren und Personen über 75 Jahren, jede weiteren 5 Min. \\(KVG\\)"
 */
proc "select-tarmed-leistung" [ val ziffer [ concat "00.0010" ] ] {
	concat "select-tarmed-leistung " $ziffer | log
	with [get-view [ t leistungen ] ] {
		get-button [ t btn_show_list ] click
		get-tab-folder | get-tab-item Tarmed | click
		with [ get-group [ t alle_leistungen ] ] {
			// disable automatic search if necessary
			if [ get-button [ t btn_automatic_search ] | get-property enablement -raw ] {
				get-button [ t btn_automatic_search ] | click
			}
			if [ $ziffer | matches "[0-9][0-9]\..*" ] {
				with [get-editbox -after [get-label Ziffer]] {
					set-text $ziffer
				}
				get-button [ t btn_execute_search ] | click
				loop [val i 0] {
					try {
							if [get-tree | get-item ".*" | get-property childCount -raw | eq 1 ] {
								concat "select-tarmed-leistung found with i " $i " childCount: <" [ get-tree | get-item ".*" | get-property childCount -raw ] ">" | log
							} -else {
								concat "select-tarmed-leistung must wait i: " $i " childCount: <" [ get-tree | get-item ".*" | get-property childCount -raw ] ">" | log
								wait 500 // TODO: Why do we need this delay? This takes time sometimes
								if [ $i | lt 10 ] { recur [$i | plus 1] }
							}
					} -catch {
						concat "select-tarmed-leistung unable to get childCount i: " $i | log
						wait 500 // TODO: Why do we need this delay? This takes time sometimes
						if [ $i | lt 10 ] { recur [$i | plus 1] }
					}
				}
				get-eclipse-window | gen-screenshot [concat $imagesDir "select-tarmed-leistung_" $ziffer ".png"]
				try {
					concat "select-tarmed-leistung via Ziffer for: " $ziffer | log
							get-tree | select [ concat ".*/.*/" $ziffer ".*" ] | double-click
					log "select-tarmed-leistung done 1"
					bool true
				} -error [ val errorObj ] -catch {
					try {
						concat "select-tarmed-leistung try 2: " $errorObj " " $ziffer ".*" | log
						get-tree | select [ concat ".*/.*/.*/" $ziffer ".*" ] | double-click
						log "select-tarmed-leistung done 2"
						bool true
					} -error [ val errorObj2 ] -catch {
						try {
							concat "select-tarmed-leistung try 3: " $errorObj2 " " $ziffer ".*" | log
							get-tree | select [ concat ".*/.*/.*/.*/" $ziffer ".*" ] | double-click
							log "select-tarmed-leistung done 3"
							bool true
						} -error [ val errorObj3 ] -catch {
							concat "select-tarmed-leistung failed adding " $ziffer " " $errorObj3| log
							bool false
						}
					}
				}
			} -else {
				concat "select-tarmed-leistung via expand-all for: " $ziffer | log
				get-tree | expand-all
				// Select just the first element
				get-tree | select [ concat ".*/.*/.*/.*" $ziffer ".*" ] | double-click
				bool true
			}
		}
	}
}

/**
 * Adds a given tarmed number. If it starts (or ends) with 0 you must call it like
 * <BR>kons-add-tarmed [ concat "00.0010"]
 * <br>this prevents converting the string to a float
*/
proc "kons-add-tarmed" [ val ziffer ]  [ val expectedItemsAdded 1 ]  {
	let [val nrItemsBefore [ kons-get-nr-invoiced-items ] ] {
		concat "kons-add-tarmed: " $ziffer " have " $nrItemsBefore " items"| log
		PressAddLeistungToKons
		select-tarmed-leistung $ziffer
		get-view [ t leistungen ] | get-group [ t alle_leistungen ] | get-tree | double-click
		// Close Leistungen
		ActivateVisibleKonsView 
		let [val nrItemsAfter [ kons-get-nr-invoiced-items ] ] {
			concat "kons-add-tarmed: " $ziffer " after have " $nrItemsAfter " items"| log
			$nrItemsBefore | plus $expectedItemsAdded | equals $nrItemsAfter | assert-true -message
				[ concat "kons-add-tarmed: " $ziffer " : After invoicing expected "  [$nrItemsBefore | plus $expectedItemsAdded ] " items but found " $nrItemsAfter]
		}
	}
	// TODO: This fails with NullPointerException and does not populate the Verrechnung
	// Not checking for this condition at the moment
	get-eclipse-window | gen-screenshot  [concat $imagesDir "kons-add-tarmed_done_" $ziffer ".png"]
}

proc "preferences-use-macros-with-TI-and-Favoriten" {
	get-preferences-menu | click
	with [get-window [ t preferences ]] {
		get-tree | select [ t Anwender_Texteingabe ]
		get-editbox -after [get-label [ t Makro_Zeichen ] ] | set-text "$"
		get-button	[ t Makros_Verrechenbar_Favoriten ] | check
		get-button [ t TI_Diagnosen ] | check
		get-button [ t apply_and_close ] | click
	}
}

proc "select-block" [ val blockName "arzt_Berichte"] {
	open-window-by-name [  t leistungen ]
	with [get-view [  t leistungen ]] {
		get-button [ t btn_show_list ]| click
		get-tab-folder | get-tab-item Block | click
		with [get-group [ t alle_leistungen ]] {
		get-editbox -after [get-label Name ] | set-text $blockName
		get-button [ t btn_execute_search ] | click
		}
	}
	try {
		get-view [  t leistungen ] | get-group [ t alle_leistungen ] | get-tree | select [ concat $blockName ".*"]
		concat "found block " $blockName | log
		bool true
	 } -catch {
		concat "Unable to find block " $blockName | log
		bool false
	}
}

proc "kons-get-nr-invoiced-items" {
	get-view [ t kons_view ] | get-table -after [get-label [t btn_accounting ] ] | get-property itemCount -raw
}

proc "kons-add-macro" [ val macroName "soap" ] {
	with [get-view [ t kons_view ]] {
		click
		with [get-editbox -after [get-label [ t lbl_heute ] ]] {
			set-text-offset 0 0
			type-text "soap$"
			let [ val content [ get-property text ] ] {
				concat "+xxx" content | log
			}
		}
/*		get-view Konsultation | get-editbox -after [get-label "23.12.2018 (heute)"] | get-property text | matches ".*S:\n"
			+ "O:\n"
			+ "A:\n"
			+ "P:.*" | verify-true
			*/
	}
}

proc "kons-add-block" [ val blockName "arzt_Besuch.*KVG.*" ] [ val expectedItemsAdded 1 ] {
	let [val nrItemsBefore [ kons-get-nr-invoiced-items ] ] {
		concat "kons-add-block: " $blockName " have " $nrItemsBefore " items"| log
		PressAddLeistungToKons
		get-tab-folder | get-tab-item Block | click
		with [get-group [ t alle_leistungen ]] {
		get-editbox -after [get-label Name ] | set-text $blockName
		try {
				get-button [ t btn_execute_search ] | click
				// wait 2000 // TODO: Why must I add this delay here?
				get-eclipse-window | gen-screenshot [concat $imagesDir "kons-add_block_before_" $blockName ".png"]
				get-tree | select [get-item -path $blockName ] | double-click
				// HIer kommt die fürchterliche Warnung folgende Leistung
				concat "kons-add-block: " $blockName " double-click done" | log
				// wait 2000 // TODO: Why must I add this delay here?
			} -catch {
			concat "kons-add-block: " $blockName " double-click failed" | log
			}
		}
		ActivateVisibleKonsView
		get-eclipse-window | gen-screenshot [concat $imagesDir "kons-added_" $blockName ".png"]
		let [val nrItemsAfter [ kons-get-nr-invoiced-items ] ] {
			concat "kons-add-block: " $blockName " after have " $nrItemsAfter " items"| log
			$nrItemsBefore | plus $expectedItemsAdded | equals $nrItemsAfter | assert-true -message
				[ concat "kons-add-block: " $blockName " After invoicing expected "  [$nrItemsBefore | plus $expectedItemsAdded ] " items but found " $nrItemsAfter]
		}
	}
}
	// ENDE

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

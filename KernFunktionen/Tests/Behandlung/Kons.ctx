--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Kons
Element-Type: context
Element-Version: 2.0
Id: _yO1KQAYtEemcp9WoDsgIvA
Runtime-Version: 2.4.0.201901240010
Save-Time: 2/12/19, 9:20 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc "KonsErstellenFallsNichtVorhanden" [ val text2enter "NixZuTUn" ] {
	if [ HasKonsSelected | eq false] {
		kons-erstellen -text2enter $text2enter
	} -else {
		log "KonsErstellenFallsNichtVorhanden: HasKonsSelected returned true"
	}
}
proc "kons-erstellen" [ val text2enter "NixZuTUn" ] {
	get-eclipse-window | get-object | save-screenshot [concat $imagesDir "Before_kons_Erstellen.png"]
	t kons_create_new | log
	get-view [ t kons_view ] | get-button [ t kons_create_new ] | click
    try {
		get-view [ t kons_view ]  |get-editbox -after [get-label [ t kons_label_today ] ] | type-text $text2enter
	} -catch {
		log "Workaround: new kons not displayed"
	// TODO: Force closing then opening Konsultation necessary?
	// TODO: Is this an RCPTT problem??
		get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ERROR_must_i_close_the_kons.png" ]
		get-view [ t kons_view ] | close
		open-window-by-name  [ t kons_view ]
		get-view [ t kons_view ]  |get-editbox -after [get-label [ t kons_label_today ] ] | type-text $text2enter
		get-eclipse-window | get-object | save-screenshot [concat $imagesDir "ERROR_reopened_kons.png" ]
	}
}

proc "HasKonsSelected" {
	try {
		// TODO: Force closing then opening Konsultation necessary?
		// TODO: Is this an RCPTT problem??
		get-view [ t kons_view ] | close
		open-window-by-name  [ t kons_view ]
		get-view [ t kons_view ]  | get-label -index 0 | get-property caption -raw | log
		get-view [ t kons_view ]  | get-link "\(.*\)" | get-property caption -raw | log
		get-view [ t kons_view ]  | get-label -index 0 | get-property caption | equals  [ t lbl_no_kons_selected ] | verify-false
		get-view [ t kons_view ]  | get-link "\(.*\)" | get-property caption  | equals "\(.*\)" | verify-true
		bool true
	} -error [val errorObj] -catch {
		log [$errorObj | get message]
		bool false
	}   
}
    

proc "PressAddDiagnosisToKons" {
	// TODO: This button should get a toolbar
	with [get-view  [ t kons_view ] | get-button -after [ get-label [ t btn_diagnosis ] ] ] {
	    click
	}
}

proc "PressAddLeistungToKons" {
	// TODO: This button should get a toolbar
	with [get-view [ t kons_view ] | get-button -after [ get-label [ t btn_accounting ] ] ] {
	    click
	}
}

proc "ActivateVisibleKonsView" {
	with [get-view [ t kons_view ] ] {
		click
	}
}

proc "kons-neue-diagnose" [ val reiter ] [ val item ] {
	ActivateVisibleKonsView
	PressAddDiagnosisToKons
	with [get-view [ t diagnosen ] ] {
	    get-group [ t alle_leistungen ] | get-editbox | set-focus
	    get-tab-folder | get-tab-item $reiter | click
	    with [get-group [ t alle_leistungen ]] {
	        get-editbox | set-focus
	        with [get-tree] {
	            select $item
	            get-item -path $item | double-click
	            get-item -path $item | click
	        }
	    }
	}

    // Close Leistungen
	ActivateVisibleKonsView 
    concat "kons-neue-diagnose added " $reiter " " $item | log
}

/**
 * Das geht eine Ewigkeit, weil rcptt sich den ganzen Artikelstamm ins Memory lädt!!
*/
proc "kons-add-artikelstamm" [ val item_short ] [ val item_long ] {
	PressAddLeistungToKons
	log "PressAddLeistungToKons done"
	with [get-view [t leistungen ] ] {
	    get-tab-folder | get-tab-item Artikelstamm | click
	    get-group  [ t alle_leistungen ] | get-editbox -after [get-label "Artikel oder Wirkstoff"] | set-focus
        get-group  [ t alle_leistungen ] | get-editbox -after [get-label [ t artikelstamm_label ] ] | set-text $item_short
		log  "before double-click"
		get-button [ t btn_execute_search ] | click
        get-group  [ t alle_leistungen ] | get-table | select $item_long | double-click
        key-type Enter
	}
    concat "kons-neuen-artikelstamm item added " $item_short " " $item_long | log
	ActivateVisibleKonsView
}

proc "kons-add-eigenleistung" [ val itemName  "Tauglichkeitsuntersuchung Führerschein" ] {
	PressAddLeistungToKons
	with [get-view [ t leistungen ] ] {
	    get-button [ t btn_show_list ] click
	    get-tab-folder | get-tab-item Eigenleistung | click
	    with [get-group [ t alle_leistungen ] ] {
	        get-editbox -after [get-label Code] | set-focus
	        get-table | select $itemName | double-click
	    }
    }
    // Close Leistungen
	ActivateVisibleKonsView 
}
proc "kons-add-tarmed" [ val itemName  "00.01.01 Konsultation" ] {
	PressAddLeistungToKons
	with [get-view [ t leistungen ] ] {
		    get-button [ t btn_show_list ] click
	    get-tab-folder | get-tab-item Tarmed | click
	    with [get-editbox -after [get-label Ziffer]] {
	        click-text
	        set-focus
	        set-text $itemName
	    }
	    get-button [ t btn_execute_search ] | click
	    // Select just the first element
	    get-tree | select ".*" | double-click
    }
    // Close Leistungen
	ActivateVisibleKonsView 
	// TODO: This fails with NullPointerException and does not populate the Verrechnung
	// Not checking for this condition at the moment
}

proc "preferences-use-macros-with-TI-and-Favoriten" {
	get-preferences-menu | click
	with [get-window Benutzervorgaben] {
	    get-tree | select "Anwender/Texteingabe"
	    get-editbox -after [get-label "Makro-Zeichen"] | set-text "$"
	    get-button "Makros Verrechenbar Favoriten" | check
	    get-button "TI Diagnosen" | check
	    get-button "Anwenden und Schließen" | click
	}
}

proc "kons-add-macro" [ val macroName "soap" ] {
	with [get-view [ t kons_view ]] {
	    click
	    with [get-editbox -after [get-label ".*heute.*"]] {
	        set-text-offset 0 0
	        type-text "soap$"
	    }
/*		get-view Konsultation | get-editbox -after [get-label "23.12.2018 (heute)"] | get-property text | matches ".*S:\n"
		    + "O:\n"
		    + "A:\n"
		    + "P:.*" | verify-true
		    */
	}
}

proc "kons-add-block" [ val blockName "arzt_Besuch.*KVG.*" ] {
	PressAddLeistungToKons
    get-tab-folder | get-tab-item Block | click
    with [get-group Alle] {
        get-editbox -after [get-label Name] | set-focus
		log "kons-add-block in Block"
		get-eclipse-window | get-object | save-screenshot [concat $imagesDir "kons_add_block_before.png"]
        get-tree | select [get-item -path $blockName ] | double-click
		get-eclipse-window | get-object | save-screenshot [concat $imagesDir "kons_add_block_after.png"]
    }
}
	// ENDE

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

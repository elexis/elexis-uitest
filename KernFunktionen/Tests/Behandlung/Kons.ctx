--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Kons
Element-Type: context
Element-Version: 2.0
Id: _yO1KQAYtEemcp9WoDsgIvA
Runtime-Version: 2.4.1.201903190000
Save-Time: 3/27/19 6:20 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc "KonsErstellenFallsNichtVorhanden" [ val text2enter "NixZuTUn" ] {
	if [ HasKonsSelected | eq false] {
		kons-erstellen -text2enter $text2enter
	} -else {
		log "KonsErstellenFallsNichtVorhanden: HasKonsSelected returned true"
	}
}
proc "kons-erstellen" [ val text2enter "NixZuTUn" ] {
	get-eclipse-window | gen-screenshot [concat $imagesDir "Before_kons_Erstellen.png"]
	t kons_create_new | log
	get-view [ t kons_view ] | get-button [ t kons_create_new ] | click
	try {
		get-view [ t kons_view ]  |get-editbox -after [get-label [ t kons_label_today ] ] | type-text $text2enter
	} -catch {
		log "Workaround: new kons not displayed"
	// TODO: Force closing then opening Konsultation necessary?
	// TODO: Is this an RCPTT problem??
		get-eclipse-window | gen-screenshot [concat $imagesDir "ERROR_must_i_close_the_kons.png" ]
		get-view [ t kons_view ] | close
		open-window-by-name  [ t kons_view ]
		get-view [ t kons_view ]  |get-editbox -after [get-label [ t kons_label_today ] ] | type-text $text2enter
		get-eclipse-window | gen-screenshot [concat $imagesDir "ERROR_reopened_kons.png" ]
	}
}

proc "HasKonsSelected" {
	try {
		// TODO: Force closing then opening Konsultation necessary?
		// TODO: Is this an RCPTT problem??
		get-view [ t kons_view ] | close
		open-window-by-name  [ t kons_view ]
		get-view [ t kons_view ]  | get-label -index 0 | get-property caption -raw | log
		get-view [ t kons_view ]  | get-link "\(.*\)" | get-property caption -raw | log
		get-view [ t kons_view ]  | get-label -index 0 | get-property caption | equals  [ t lbl_no_kons_selected ] | verify-false
		get-view [ t kons_view ]  | get-link "\(.*\)" | get-property caption  | equals "\(.*\)" | verify-true
		bool true
	} -error [val errorObj] -catch {
		log [$errorObj | get message]
		bool false
	}   
}
	

proc "PressAddDiagnosisToKons" {
	// TODO: This button should get a toolbar
	with [get-view  [ t kons_view ] | get-button -after [ get-label [ t btn_diagnosis ] ] ] {
		click
	}
}

proc "PressAddLeistungToKons" {
	// TODO: This button should get a toolbar
	try {
		with [get-view [ t kons_view ] | get-button -after [ get-label [ t btn_accounting ] ] ] {
			click
		}
	} -error [val errorObj] -catch {
		concat "PressAddLeistungToKons failed: " $errorObj | log
		close-window-if-open "Warnung.*"
	}
}

proc "ActivateVisibleKonsView" {
	concat "ActivateVisibleKonsView " [ t kons_view ] | log
	try {
		get-view [ t kons_view ] | get-editbox | set-focus
		log "ActivateVisibleKonsView no problems"
		bool true
	}  -error [val errorObj]  -catch {
		concat "ActivateVisibleKonsView " [ t kons_view ]  " has problems." $errorObj " Trying to close warning" | log
		close-window-if-open "Warnung.*"
		get-view [ t kons_view ] | get-editbox | set-focus
		bool false
	}
}

proc "kons-neue-diagnose" [ val reiter ] [ val item ] {
	ActivateVisibleKonsView
	PressAddDiagnosisToKons
	with [get-view [ t diagnosen ] ] {
		get-group [ t alle_leistungen ] | get-editbox | set-focus
		get-tab-folder | get-tab-item $reiter | click
		with [get-group [ t alle_leistungen ]] {
		get-editbox | set-focus
		with [get-tree] {
			select $item
			get-item -path $item | double-click
			get-item -path $item | click
		}
		}
	}

	// Close Leistungen
	ActivateVisibleKonsView 
	concat "kons-neue-diagnose added " $reiter " " $item | log
}

/**
 * Das geht eine Ewigkeit, weil rcptt sich den ganzen Artikelstamm ins Memory lädt!!
 * <br>Der Name muss so gewählt werden, dass kein hellblau eingefärbter Artikel als erster in der
 * Auswahl erscheint, da diese nicht verrechnet werden können
*/
proc "kons-add-artikelstamm" [ val item_short ] [ val item_long ] {
	PressAddLeistungToKons
	log "PressAddLeistungToKons done"
	with [get-view [t leistungen ] ] {
		get-tab-folder | get-tab-item Artikelstamm | click
		get-group  [ t alle_leistungen ] | get-editbox -after [get-label "Artikel oder Wirkstoff"] | set-focus
		get-group  [ t alle_leistungen ] | get-editbox -after [get-label [ t artikelstamm_label ] ] | set-text $item_short
		log  "before double-click"
		get-button [ t btn_execute_search ] | click
		get-group  [ t alle_leistungen ] | get-table | select $item_long | double-click
		key-type Enter
	}
	concat "kons-neuen-artikelstamm item added " $item_short " " $item_long | log
	ActivateVisibleKonsView
}

proc "kons-add-eigenleistung" [ val itemName  "Tauglichkeitsuntersuchung Führerschein" ] {
	PressAddLeistungToKons
	with [get-view [ t leistungen ] ] {
		get-button [ t btn_show_list ] click
		get-tab-folder | get-tab-item Eigenleistung | click
		with [get-group [ t alle_leistungen ] ] {
		get-editbox -after [get-label Code] | set-focus
		get-table | select $itemName | double-click
		}
	}
	// Close Leistungen
	ActivateVisibleKonsView 
}

/**
 * Select a tarmed by it position. As RCPTT replace a leading 00.0 by 0.0
 * you must enter values like %00.0010 as we will strip all %
 * RCPTT is only to able to select a single item if the full path is given, eg.
 * "00 Grundleistungen \\(KVG\\)/00.01 Allgemeine Grundleistungen \\(KVG\\)/00.01.01 Konsultation, Besuch, Wegentschädigung u.a. \\(KVG\\)/00.0025 \\+ Konsultation bei Kindern unter 6 Jahren und Personen über 75 Jahren, jede weiteren 5 Min. \\(KVG\\)"
 */
proc "select-tarmed-leistung" [ val ziffer "%00.0010" ] [ val fullname "xxx" ] {
	concat "select-tarmed-leistung " $ziffer " full: " $fullname | log
	open-window-by-name "Codes"
	with [get-view Codes] {
		get-tab-folder | get-tab-item Tarmed | click
		get-tab-folder | key-type "M1+m"
		let [ val stripped [ $ziffer | split -sep "%" -omitEmptyStrings true ] ] {
		    with [get-editbox -after [get-label Ziffer]] {
		        set-text $stripped
		    }
		    get-button "Suche jetzt ausführen" | click
		    try {
	// TODO: RCPTT is only to able to select a single item if the full path is given, eg.
	// get-tree | select "00 Grundleistungen \\(KVG\\)/00.01 Allgemeine Grundleistungen \\(KVG\\)/00.01.01 Konsultation, Besuch, Wegentschädigung u.a. \\(KVG\\)/00.0025 \\+ Konsultation bei Kindern unter 6 Jahren und Personen über 75 Jahren, jede weiteren 5 Min. \\(KVG\\)"
	// else it fails eg.
	// get-tree | select  ".*Grundleistungen.*" -all true
				get-tree | expand-all
				get-eclipse-window | gen-screenshot [concat $imagesDir "select-tarmed_" $stripped ".png"]
				get-tree | get-item -path  $fullname | get-property caption -raw | log
				get-tree | get-item -path $fullname | select-item | get-property caption -raw | matches ".+"
				concat  "select-tarmed-leistung " $stripped " passed" | log
				bool true
		    } -error [ val errorObj ] -catch {
				concat  "select-tarmed-leistung " $stripped " checking for Fehler: error " $errorObj | log
				bool false
			}
	    }
	}
}

/**
 * Adds a given tarmed number. If it starts (or ends) with 0 you must call it like
 * <BR>kons-add-tarmed [ concat "00.0010"]
 * <br>this prevents converting the string to a float
*/
proc "kons-add-tarmed" [ val ziffer ] {
	PressAddLeistungToKons
	with [get-view [ t leistungen ] ] {
		get-button [ t btn_show_list ] click
		get-tab-folder | get-tab-item Tarmed | click
		// disable automatic search if necessary
		with [ get-group Alle ] {
			if [ get-button [ t btn_automatic_search ] | get-property enablement -raw ] {
				get-button [ t btn_automatic_search ] | click
			}
			if [ $ziffer | matches "[0-9][0-9]\..*" ] {
				with [get-editbox -after [get-label Ziffer]] {
					set-text $ziffer
				} 
				get-button [ t btn_execute_search ] | click
				loop [val i 0] {
					try {
							if [ or [get-tree | get-item ".*" | get-property childCount -raw | eq 1 ] [ $i | gt 10 ] ] {
								concat "kons-add-tarmed found with i " $i " childCount: <" [ get-tree | get-item ".*" | get-property childCount -raw ] ">" | log
							} -else {
								concat "kons-add-tarmed must wait i: " $i " childCount: <" [ get-tree | get-item ".*" | get-property childCount -raw ] ">" | log
								wait 500 // TODO: Why do we need this delay? This takes time sometimes
								recur [$i | plus 1]
							}
					} -catch {
						concat "kons-add-tarmed unable to get childCount i: " $i | log
						wait 500 // TODO: Why do we need this delay? This takes time sometimes
						recur [$i | plus 1]
					}
				}
				get-eclipse-window | gen-screenshot [concat $imagesDir "kons-add-tarmed_" $ziffer ".png"]
				try {
					concat "kons-add-tarmed via Ziffer for: " $ziffer | log
							get-tree | select [ concat ".*/.*/" $ziffer ".*" ] | double-click
					log "kons-add-tarmed done 1"
				} -error [ val errorObj ] -catch {
					try {
						concat "kons-add-tarmed try 2: " $errorObj " " $ziffer ".*" | log
						get-tree | select [ concat ".*/.*/.*/" $ziffer ".*" ] | double-click
						log "kons-add-tarmed done 2"
					} -error [ val errorObj2 ] -catch {
						try {
							concat "kons-add-tarmed try 3: " $errorObj2 " " $ziffer ".*" | log
							get-tree | select [ concat ".*/.*/.*/.*/" $ziffer ".*" ] | double-click
							log "kons-add-tarmed done 3"
						} -error [ val errorObj3 ] -catch {
							concat "kons-add-tarmed failed adding " $ziffer " " $errorObj3| log
						}
					}
				}
			} -else {
				concat "kons-add-tarmed via expand-all for: " $ziffer | log
				get-tree | expand-all
				// Select just the first element
				get-tree | select [ concat ".*/.*/.*/.*" $ziffer ".*" ] | double-click
			}
		}
	}
	// Close Leistungen
	ActivateVisibleKonsView 
	// TODO: This fails with NullPointerException and does not populate the Verrechnung
	// Not checking for this condition at the moment
	get-eclipse-window | gen-screenshot  [concat $imagesDir "kons-add-tarmed_done_" $ziffer ".png"]
}

proc "preferences-use-macros-with-TI-and-Favoriten" {
	get-preferences-menu | click
	with [get-window Benutzervorgaben] {
		get-tree | select "Anwender/Texteingabe"
		get-editbox -after [get-label "Makro-Zeichen"] | set-text "$"
		get-button "Makros Verrechenbar Favoriten" | check
		get-button "TI Diagnosen" | check
		get-button "Anwenden und Schließen" | click
	}
}

proc "select-block" [ val blockName "arzt_Berichte"] {
	open-window-by-name Leistungen
	with [get-view Leistungen] {
		get-button "Liste anzeigen" | click
		get-tab-folder | get-tab-item Block | click
		with [get-group Alle] {
		get-editbox -after [get-label Name] | set-text $blockName
		get-button "Suche jetzt ausführen" | click
		}
	}
	try {
		get-view Leistungen | get-group Alle | get-tree | select [ concat $blockName ".*"]
		concat "found block " $blockName | log
		bool true
	 } -catch {
		concat "Unable to find block " $blockName | log
		bool false
	}
}

proc "kons-add-macro" [ val macroName "soap" ] {
	with [get-view [ t kons_view ]] {
		click
		with [get-editbox -after [get-label ".*heute.*"]] {
		set-text-offset 0 0
		type-text "soap$"
		}
/*		get-view Konsultation | get-editbox -after [get-label "23.12.2018 (heute)"] | get-property text | matches ".*S:\n"
			+ "O:\n"
			+ "A:\n"
			+ "P:.*" | verify-true
			*/
	}
}

proc "kons-add-block" [ val blockName "arzt_Besuch.*KVG.*" ] {
	concat "kons-add-block: " $blockName | log
	PressAddLeistungToKons
	get-tab-folder | get-tab-item Block | click
	with [get-group Alle] {
	get-editbox -after [get-label Name] | set-text $blockName
	try {
			get-button [ t btn_execute_search ] | click
			// wait 2000 // TODO: Why must I add this delay here?
			get-eclipse-window | gen-screenshot [concat $imagesDir "kons-add_block_before_" $blockName ".png"]
			get-tree | select [get-item -path $blockName ] | double-click
			// HIer kommt die fürchterliche Warnung folgende Leistung
			concat "kons-add-block: " $blockName " double-click done" | log
			// wait 2000 // TODO: Why must I add this delay here?
		} -catch {
		concat "kons-add-block: " $blockName " double-click failed" | log
		}
	}
	ActivateVisibleKonsView
	get-eclipse-window | gen-screenshot [concat $imagesDir "kons-added_" $blockName ".png"]
	concat "kons-add-block: " $blockName " done" | log
}
	// ENDE

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

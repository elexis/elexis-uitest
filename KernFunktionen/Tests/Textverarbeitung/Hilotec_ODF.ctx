--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Hilotec_ODF
Element-Type: context
Element-Version: 2.0
Id: _LkBSkBsDEembU9g-6bJhVg
Runtime-Version: 2.4.1.201903190000
Save-Time: 3/29/19 11:54 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

global [val hilotecOdfOutDir $pdfDir ] -override

proc ConfigureHilotecODF_TextVerarbeitung {
	get-preferences-menu | click
	with [get-window [ t preferences ]] {
		get-tree | select [ t Textverarbeitung ]
		// Button name various on number of installed text plugins!
		// There reaplce 0 :, etc by .*
	  	get-window [ t preferences ] | gen-screenshot [concat $imagesDir "select-hilotecODF.png" ]
		get-button ".*Hilotec-OpenDocument" | check
		get-tree | select "Hilotec-OpenDocument"
		with [get-editbox -after [get-label "OpenDocument Editor"]] {
			set-text "/usr/bin/lowriter"
		}
		with [get-editbox -after [get-label "Argumente zum Drucken"]] {
			set-text [ concat "--headless --convert-to pdf --outdir " $hilotecOdfOutDir ]
		}
		with [get-editbox -after [get-label "Argumente fuer PDF-Konvertierung"]] {
			set-text [ concat "--headless --convert-to pdf --outdir " $hilotecOdfOutDir ]
		}
		with [get-editbox -after [get-label "PDF-Konverter"]] {
			set-text "/usr/bin/lowriter"
		}
		get-button "Wrapper Skript aktivieren" | uncheck
	  	get-window [ t preferences ] | gen-screenshot [concat $imagesDir "config-hilotecODF.png" ]
		get-button [ t apply_and_close ] | click
	}
}

proc ImportHilotecODF_Vorlagen [ val vorlagen [ concat $rcpttDir "vorlagen/AUF-Zeugnis.odt" ] ] {
	concat "ImportHilotecODF_Vorlagen should import: " $vorlagen | log
	get-file $vorlagen | get exists | assert-true
	open-window-by-name [ t Dokumente_Textvorlagen_Management ]
	set-dialog-result File $vorlagen
	get-view "Textvorlagen Management" | get-button "Beliebige Vorlage.*importieren" | click
	try -command {
		get-window Systemvorlage | get-button [ t yes ] | click
	} -catch { 
		log "Keine Systemvorlage"
	}
	try -command {
		with [get-window -class TextTemplateImportConflictDialog] {
			get-button "Ersetzen.*" | click
			get-button OK | click
		}
		concat "Vorlage: " $vorlagen " existiert bereits, wurde ersetzt" | log
	
	} -catch { 
		log "Vorlage existiert noch nicht"
	}
	OpenAndResetStartPerspektive
}
// Ende
//
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--

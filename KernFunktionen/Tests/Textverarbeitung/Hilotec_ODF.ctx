--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: Hilotec_ODF
Element-Type: context
Element-Version: 2.0
Id: _LkBSkBsDEembU9g-6bJhVg
Save-Time: 5/4/20 9:05 AM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

global [val hilotecOdfOutDir $pdfDir ] -override

proc EnsureLibreOffficIsInstalled {
	log "EnsureLibreOffficIsInstalled: Check whether libreoffice ist installed"
	if [ get-by-os  -win false -macosx false -linux true ] {
		let [ val result [ exec-process -command "libreoffice" --args "--version"  ] ] {
			concat "EnsureLibreOffficIsInstalled: libreoffice version returned " [ $result | get  exitCode ] " out " 
				[ $result | get  stdout ] | log
			concat "EnsureLibreOffficIsInstalled: libreoffice version returned " [ $result | get  exitCode ] " out " 
			$result | get  exitCode | equals 0 | assert-true
			let [ val result [ exec-process -command "which" --args "libreoffice"  ] ] {
				global [val lowriter [ invoke-static "org.apache.commons.lang3" "org.apache.commons.lang3.StringUtils" "chop" [ $result | get  stdout ] ]  ] -override
				concat "EnsureLibreOffficIsInstalled: lowriter is now set to: " $lowriter ": done"| log
			}
		}
		log "EnsureLibreOffficIsInstalled: libreoffice ist installed (LINUX)"
	}
}

proc CloseLibreOffice {
	if [ get-by-os  -win false -macosx false -linux true ] {
		exec-process -command killall -args oosplash -ignoreExitCode -ignoreStderr
	} -else {
		log "I do not now howto close CloseLibreOffice under windows or macosx"
	}
}

CloseLibreOffice // Ensure that we have no LibreOffice process open when running our tests

proc ConfigureHilotecODF_TextVerarbeitung {
	EnsureLibreOffficIsInstalled
	get-preferences-menu | click
	with [get-window [ t preferences ]] {
		get-tree | select [ t Textverarbeitung ]
		// Button name various on number of installed text plugins!
		// There replace 0 :, etc by .*
	  	get-window [ t preferences ] | gen-screenshot [concat $imagesDir "select-hilotecODF.png" ]
	    with [get-button "Brief extern speichern (gleicher UNC Pfad auf allen Stationen)"] {
	        check
	    }
	    with [get-editbox -after [get-label [ t pref_externes_textprogramm ] ] ] {
	        set-text [ concat  [get-user-dir] "/extern" ]
	    }
	    with [get-button [ t pref_texte_lokal_editieren ] ] {
	        check
	    }
	    get-button ".*Hilotec-OpenDocument" | click
		get-tree | select "Hilotec-OpenDocument"
		if [ get-by-os  -win true -macosx false -linux false ] {
			global [val lowriter "C:\\Program Files\\LibreOffice\\program\\swriter" ] -override
		} // else lowriter variable set mby LOWriterMustBeInstalled
		with [get-editbox -after [get-label "OpenDocument Editor"]] {
			set-text $lowriter
		}
		with [get-editbox -after [get-label "Argumente zum Drucken"]] {
			set-text [ concat "--headless --convert-to pdf --outdir " $hilotecOdfOutDir ]
		}
		with [get-editbox -after [get-label "Argumente fuer PDF-Konvertierung"]] {
			set-text [ concat "--headless --convert-to pdf --outdir " $hilotecOdfOutDir ]
		}
		with [get-editbox -after [get-label "PDF-Konverter"]] {
			set-text $lowriter
		}
		get-button "Wrapper Skript aktivieren" | uncheck
	  	get-window [ t preferences ] | gen-screenshot [concat $imagesDir "config-hilotecODF.png" ]
		get-button [ t apply_and_close ] | click
	}
}

proc ImportHilotecODF_Vorlagen [ val vorlage [ concat $rcpttDir "elexis-vorlagen-master/openoffice/AUF-Zeugnis.odt" ] ] {
	concat "ImportHilotecODF_Vorlagen should import: " $vorlage | log
	get-file $vorlage | get exists | assert-true -message [ concat "File " $vorlage " not found" ]
	open-window-by-name [ t Dokumente_Textvorlagen_Management ]
	set-dialog-result File $vorlage
	get-view "Textvorlagen Management" | get-button "Beliebige Vorlage.*importieren" | click
	try -command {
		get-window Systemvorlage | get-button [ t yes ] | click
	} -catch { 
		log "Keine Systemvorlage"
	}
	try -command {
		with [get-window -class TextTemplateImportConflictDialog] {
			get-button "Ersetzen.*" | click
			get-button OK | click
		}
		concat "Vorlage: " $vorlage " existiert bereits, wurde ersetzt" | log
	
	} -catch { 
		log "Vorlage existiert noch nicht"
	}
	OpenAndResetStartPerspektive
}
/* Mit /ohne Adresse
with [get-view "Textvorlagen Management" | get-table] {
    select Liste -column "Name der Vorlage"
    get-cell 1 4 | uncheck
}
with [get-view "Textvorlagen Management" | get-table] {
    select Liste -column "Name der Vorlage"
    get-cell 1 4 | check
}
*/
proc TextVorlagenEntfernen [ val name Liste ] {
	open-window-by-name [ t Textvorlagen_Management ]
	get-view [ t Textvorlagen_Management ] | get-table |
		select $name -column [ t Name_der_Vorlage ] |
		get-menu [ t Delete_template ]
}

// Ende
//
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
